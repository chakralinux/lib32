# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=boost-libs
pkgname=lib32-$_pkgbasename
pkgver=1.48.0
_boostver=1_48_0
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime (32 bit)"
arch=('x86_64')
url="http://www.boost.org"
license=('custom')
groups=('lib32')
depends=('lib32-bzip2' 'lib32-zlib' 'lib32-gcc-libs')
makedepends=('icu' 'bzip2' 'lib32-zlib' 'openmpi' 'gcc-multilib')
source=(http://downloads.sourceforge.net/sourceforge/boost/boost_${_boostver}.tar.gz)
md5sums=('313a11e97eb56eb7efd18325354631be')

_stagedir="${srcdir}/stagedir"

build()
{
  export CC="gcc"
  export CFLAGS="-m32"
  export CXX="g++"
  export CXXFLAGS="-m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # set python path for bjam
  cd "${srcdir}/boost_${_boostver}/tools"
#  echo "using python : 2.7 : /usr/bin/python2 ;" >> build/v2/user-config.jam
#  echo "using python : 3.2 : /usr/bin/python3.2 : /usr/include/python3.2mu :
#  /usr/lib32 ;" >> build/v2/user-config.jam
#  echo "using mpi ;" >> build/v2/user-config.jam

  # build bjam
  cd "${srcdir}/boost_${_boostver}/tools/build/v2/engine"
  ./build.sh cc

  _bindir="bin.linuxx86"

  install -d "${_stagedir}"/usr/bin
  install ${_bindir}/bjam "${_stagedir}"/usr/bin/bjam

  # build bcp
  cd "${srcdir}/boost_${_boostver}/tools/bcp"
  ../build/v2/engine/${_bindir}/bjam --toolset=gcc
  install -m755 "${srcdir}/boost_${_boostver}/dist/bin/bcp" \
                ${_stagedir}/usr/bin/bcp

  # build libs
  cd "${srcdir}/boost_${_boostver}"

  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=single,multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  ./tools/build/v2/engine/${_bindir}/bjam \
      release debug-symbols=off threading=multi \
      runtime-link=shared link=shared,static \
      cflags=-fno-strict-aliasing \
      toolset=gcc \
      address-model=32 \
      --without-python \
      --without-mpi \
      --prefix="${_stagedir}" \
      -sTOOLS=gcc \
      --layout=system \
      ${MAKEFLAGS} \
      install
}

package() 
{
  install -d "${pkgdir}/usr/lib32"

  #shared libs
  cp -r "${_stagedir}"/lib/*.so{,.*} "${pkgdir}/usr/lib32/"
                     
  # license
  install -D -m644 "${srcdir}/boost_${_boostver}/LICENSE_1_0.txt" \
                   "${pkgdir}"/usr/share/licenses/lib32-boost-libs/LICENSE_1_0.txt
}

