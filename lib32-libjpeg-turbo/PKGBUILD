# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libjpeg-turbo
pkgname=lib32-$_pkgbasename
pkgver=1.3.0
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression (lib32)'
arch=('x86_64')
url='http://libjpeg-turbo.virtualgl.org/'
license=('GPL'
         'custom')
makedepends=('nasm')
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('nasm' gcc-multilib)
provides=('lib32-libjpeg=8.0.2')
conflicts=('lib32-libjpeg')
replaces=('lib32-libjpeg')
options=('!libtool')
options=('!libtool')
source=("http://sourceforge.net/projects/$_pkgbasename/files/$pkgver/$_pkgbasename-$pkgver.tar.gz"
        'cve-2013-6629.patch')
sha512sums=('4d34c3c5f2cdd70b2a3d1b55eeb4ce59cb3d4b8d22bb6d43c2ec844b7eb5685b55a9b1b46ad2bc5f2756b5f5535ccad032791c3b932af9c1efc502aa5e701053'
            'c5f063f87305ab48cb40c243df9e7a307efedc02eef8eee65e9ca5006676d5257a6d1dc7a32ff77a2486c8be8792b4f5431d18e46759ad63c182b8332a736099')

prepare() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    patch -i ../cve-2013-6629.patch # FS#38094
}

build() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"


    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    sed -i "s|NAFLAGS='-felf64 -DELF -D__x86_64__'|NAFLAGS='-felf32 -DELF -D__x86_64__'|" configure

    # Create libjpeg6
    ./configure \
        --prefix=/usr \
        --with-jpeg6 \
        --mandir=/usr/share/man \
	--libdir=/usr/lib32 \
	--without-simd
    make
    mkdir -p ${srcdir}/libjpeg6
    cp -d .libs/libjpeg.so.6* ${srcdir}/libjpeg6
    rm -R .libs

    # Create libjpeg8
    ./configure \
        --prefix=/usr \
        --with-jpeg8 \
        --mandir=/usr/share/man \
	--libdir=/usr/lib32 \
	--without-simd
    make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir/" install
  cp -vd $srcdir/libjpeg6/libjpeg.so.6* $pkgdir/usr/lib32
  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
