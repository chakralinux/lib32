# Lib32 Packages for Chakra, part of chakra-project.org
# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=3.2.2
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
options=('libtool')
depends=(lib32-{gcc-libs,readline,libgcrypt,zlib,nettle,libtasn1})
makedepends=('gcc-multilib' 'lib32-libidn')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.2/${_pkgbasename}-${pkgver}.tar.xz{,.sig}
        tls_fix.diff)
md5sums=('9dd691ad1ccdb7386029809afef6b5ea'
         SKIP
         '1bbf5bfb4e1420fd61c75e14347340fc')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}
  
    # fix broken TLS connections
  patch -Np1 -i ../tls_fix.diff
  
  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --with-zlib \
              --disable-guile \
              --disable-static \
              --disable-valgrind-tests --disable-hardware-acceleration

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
