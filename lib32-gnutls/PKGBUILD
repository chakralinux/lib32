# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>

_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=3.3.11
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
depends=(lib32-libtasn1
         lib32-nettle
         lib32-p11-kit
         lib32-zlib
         $_pkgbasename=$pkgver)
makedepends=('gcc-multilib' 'lib32-libidn')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.3/${_pkgbasename}-${pkgver}.tar.xz{,.sig})
md5sums=('b657e3010c10cae2244e7ce79ee3d446'
         'SKIP')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  #cd ${srcdir}/${_pkgbasename}-${pkgver}
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --with-zlib \
              --disable-guile \
              --disable-static \
              --disable-valgrind-tests --disable-hardware-acceleration

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
