# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=smpeg2
pkgname=lib32-$_pkgbasename
pkgver=412
pkgrel=1
pkgdesc="SDL2 MPEG Player Library (32-bit)"
arch=('i686' 'x86_64')
url="http://icculus.org/smpeg/"
license=('LGPL')
depends=('lib32-sdl2' $_pkgbasename=$pkgver)
makedepends=('lib32-mesa' 'lib32-glu' 'svn' 'gcc-multilib')
optdepends=('lib32-glu: to use glmovie')
options=('!libtool')
source=(svn://svn.icculus.org/smpeg/trunk#revision=$pkgver)
md5sums=('SKIP')

build() {
  cd ${srcdir}/trunk
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  sed -i s/SDL_CONFIG/SDL2_CONFIG/g smpeg2-config.in
  ./autogen.sh
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --disable-static \
              --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/trunk
  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
