# Lib32 Packages for Chakra, part of chakra-project.org

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=0.9.37
pkgrel=2
pkgdesc="OpenType text shaping engine. (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('lib32-icu' 'lib32-glib2' 'lib32-freetype2' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
source=("http://www.freedesktop.org/software/harfbuzz/release/${_pkgbasename}-${pkgver}.tar.bz2")
md5sums=('bfe733250e34629a188d82e3b971bc1e')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  autoreconf --force --install --verbose
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-silent-rules \
              --with-glib \
	      --with-freetype \
	      --with-icu \
	      --without-cairo \
              --without-graphite2
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
