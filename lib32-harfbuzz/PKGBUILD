# Lib32 Packages for Chakra, part of chakra-project.org
#s

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=0.9.26
pkgrel=1
pkgdesc="OpenType text shaping engine. (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('lib32-icu' 'lib32-glib2' 'lib32-freetype2' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib' 'lib32-cairo')
source=("http://www.freedesktop.org/software/harfbuzz/release/${_pkgbasename}-${pkgver}.tar.bz2")
md5sums=('a2b6cbd437f22dd5dc93d377696c9204')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  #autoreconf --force --install --verbose
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --with-glib \
	      --with-freetype \
	      --with-cairo \
	      --with-icu \
              --without-graphite2
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
