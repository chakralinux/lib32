# Lib32 packages for Chakra

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=0.9.9
pkgrel=1
pkgdesc="OpenType text shaping engine. (32-bit)"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('lib32-icu' 'lib32-glib2' 'lib32-freetype2' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=(!libtool)
source=(http://www.freedesktop.org/software/harfbuzz/release/${_pkgbasename}-${pkgver}.tar.bz2
        remove-graphite.patch)
sha256sums=('3d8dd4ebbc38da011485ef181f8ff072b4b0d7e305bbda19b355f217d25169cf'
            '198861db452ba6a0223763540783a7f682195b3f08043272673dbdc9d0ca1617')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  patch -p0 -i "$srcdir/remove-graphite.patch"

  ./autogen.sh --prefix=/usr --libdir=/usr/lib32 --disable-silent-rules
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
