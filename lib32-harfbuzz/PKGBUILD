# Lib32 packages for Chakra

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=0.9.18
pkgrel=2
pkgdesc="OpenType text shaping engine. (32-bit)"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('lib32-icu' 'lib32-glib2' 'lib32-freetype2' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=(!libtool)
source=(http://www.freedesktop.org/software/harfbuzz/release/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('fef0fbbc6d6492cb4074a5e26a699d87d88bef8adc25dda62af6077735a7d908')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  autoreconf --force --install --verbose
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-silent-rules \
              --with-graphite2=no \
              --without-cairo
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
