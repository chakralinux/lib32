# Lib32 Packages for Chakra, part of chakra-project.org
#
# Maintainer:  AlmAck <almack@chakra-project.org>
# Contributor: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=cracklib
pkgname=lib32-$_pkgbasename
pkgver=2.9.6
pkgrel=1
pkgdesc="Password Checking Library (32 bit)"
arch=('x86_64')
url="http://sourceforge.net/projects/cracklib"
license=('LGPL')
depends=('lib32-glibc' 'lib32-zlib' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
source=(https://github.com/cracklib/cracklib/releases/download/${_pkgbasename}-${pkgver}/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('c52f463585d85924b28cdc1e373ae06d')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32 --without-python CC='gcc -m32'
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/usr"/{include,sbin,share}
}
