# Lib32 Packages for Chakra, part of chakra-project.org
#
# Maintainer:  AlmAck <almack@chakra-project.org>
# Contributor: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=cracklib
pkgname=lib32-$_pkgbasename
pkgver=2.8.18
pkgrel=1
pkgdesc="Password Checking Library (32 bit)"
arch=('x86_64')
url="http://sourceforge.net/projects/cracklib"
license=('LGPL')
depends=('lib32-glibc' 'lib32-zlib' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz)
md5sums=('79053ad8bc714a44cd660cb12116211b')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32 --without-python CC='gcc -m32'
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/usr"/{include,sbin,share}
}
