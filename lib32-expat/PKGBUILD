# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=expat
pkgname=lib32-$_pkgbasename
pkgver=2.0.1
pkgrel=1
pkgdesc="An XML Parser library written in C (32 bit)"
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
makedepends=('gcc-multilib')
depends=('lib32-glibc' "${_pkgbasename}")
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/expat/${_pkgbasename}-${pkgver}.tar.gz
        CVE-2009-3560.patch
        CVE-2009-3720.patch)
md5sums=('ee8b492592568805593f81f8cdf2a04c'
         '50603cac0f03aabc7087415251f592be'
         'f3eeb796f28945899216b815e5901996')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i $srcdir/CVE-2009-3560.patch
  patch -Np1 -i $srcdir/CVE-2009-3720.patch
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  ./configure --prefix=/usr --libdir=/usr/lib32 --mandir=/usr/share/man 
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share/man}
}
