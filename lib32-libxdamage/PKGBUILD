# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Calà <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxdamage
pkgname=lib32-$_pkgbasename
pkgver=1.1.2
pkgrel=2
pkgdesc="X11 damaged region extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes>=4.0.4' $_pkgbasename)
makedepends=('pkg-config' 'damageproto>=1.2.0' gcc-multilib)
options=('!libtool')
source=(${url}/releases/individual/lib/libXdamage-${pkgver}.tar.bz2)
md5sums=('b42780bce703ec202a33e5693991c09d')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXdamage-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXdamage-${pkgver}"

  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
