#
# Lib32 Packages for Chakra, part of chakra-project.org
#

_pkgbasename=nspr
pkgname=lib32-$_pkgbasename
pkgver=4.10.6
pkgrel=1
pkgdesc="Netscape Portable Runtime (32-bit)"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('zip' 'gcc-multilib')
options=('!emptydirs')
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('6ab81e8d508457905223eaf4ed0a973b')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug \
      --enable-32bit
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  chmod 644 "$pkgdir"/usr/lib32/*.a
  ln -s nspr.pc "$pkgdir/usr/lib32/pkgconfig/mozilla-nspr.pc"
  rm -r "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
        "$pkgdir/usr/include/nspr/md"
  rm -rf "${pkgdir}"/usr/{bin,share,include}
}
