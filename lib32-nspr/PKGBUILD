# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=nspr
pkgname=lib32-$_pkgbasename
pkgver=4.8.6
pkgrel=1
pkgdesc="Netscape Portable Runtime (32-bit)"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('zip' 'gcc-multilib')
options=(!emptydirs)
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${_pkgbasename}-${pkgver}.tar.gz
        nspr.pc.in)
md5sums=('592c275728c29d193fdba8009165990b'
         'bce1611f3117b53fc904cab549c09967')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  unset CFLAGS
  unset CXXFLAGS
  ./mozilla/nsprpub/configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug ${confflags}
  make

  NSPR_LIBS=`./config/nspr-config --libs`
  NSPR_CFLAGS=`./config/nspr-config --cflags`
  NSPR_VERSION=`./config/nspr-config --version`
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  sed "${srcdir}/nspr.pc.in" -e "s,%libdir%,/usr/lib32," \
  	-e "s,%prefix%,/usr," \
	-e "s,%exec_prefix%,/usr/bin," \
	-e "s,%includedir%,/usr/include/nspr," \
	-e "s,%NSPR_VERSION%,${NSPR_VERSION}," \
	-e "s,%FULL_NSPR_LIBS%,${NSPR_LIBS}," \
	-e "s,%FULL_NSPR_CFLAGS%,${NSPR_CFLAGS}," > "${srcdir}/nspr.pc"
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -D "${srcdir}/nspr.pc"  "${pkgdir}"/usr/lib32/pkgconfig/nspr.pc
  chmod 644 "${pkgdir}/usr/lib32/pkgconfig/nspr.pc"
  ln -sf nspr.pc "${pkgdir}/usr/lib32/pkgconfig/mozilla-nspr.pc"

  chmod 644 ${pkgdir}/usr/lib32/*.a

  rm -rf "${pkgdir}"/usr/{bin,share,include}
}
