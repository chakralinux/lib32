_pkgbasename=fluidsynth
pkgname=lib32-${_pkgbasename}
pkgver=1.1.6
pkgrel=5
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications (32bit)"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=("$_pkgbasename=$pkgver" 'lib32-glib2' 'lib32-jack' 'lib32-alsa-lib' 'lib32-libpulse')
makedepends=('cmake' 'gcc' 'lib32-gcc-libs')
license=('LGPL')
source=("http://downloads.sourceforge.net/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz")
md5sums=('ae5aca6de824b4173667cbd3a310b263')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -Denable-ladspa=ON \
          -DLIB_SUFFIX=32
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
 
