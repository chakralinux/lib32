_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.9.6+3+g5af594d8
pkgrel=1
pkgdesc="XML parsing library, version 2 (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-zlib' 'lib32-readline' 'lib32-ncurses' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
_commit=5af594d8bc55121ae454cba4d05793d1db7ff612  # master
source=(git://git.gnome.org/libxml2#commit=$_commit
        http://www.w3.org/XML/Test/xmlts20080827.tar.gz)
md5sums=('SKIP'
         'ae3d1ebe000a3972afa104ca7f0e1b4a')

 pkgver() {
  cd $_pkgbasename
  git describe --long | sed -e 's/-/+/g' -e 's/^v//'
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename

  autoreconf -fi
  ./configure --prefix=/usr --with-threads --with-history --libdir=/usr/lib32 --without-lzma --without-python

  # added without-python : https://bugzilla.redhat.com/show_bug.cgi?id=139911

  make
}

package() {
  cd $_pkgbasename
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
