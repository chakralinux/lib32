# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.9.4+12+ge905f08
pkgrel=1
pkgdesc="XML parsing library, version 2 (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-zlib>=1.2.4' 'lib32-readline>=6.1' 'lib32-ncurses>=5.7' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
options=('!libtool')
_commit=e905f08123e4a6e7731549e6f09dadff4cab65bd  # master
source=(git://git.gnome.org/libxml2#commit=$_commit
        http://www.w3.org/XML/Test/xmlts20080827.tar.gz)
md5sums=('SKIP'
         'ae3d1ebe000a3972afa104ca7f0e1b4a')

 pkgver() {
  cd $_pkgbasename
  git describe --long | sed -e 's/-/+/g' -e 's/^v//'
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename

  autoreconf -fi
  ./configure --prefix=/usr --with-threads --with-history --libdir=/usr/lib32 --without-lzma --without-python

  # added without-python : https://bugzilla.redhat.com/show_bug.cgi?id=139911

  make
}

package() {
  cd $_pkgbasename
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
