# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxmu
pkgname=lib32-$_pkgbasename
pkgver=1.1.0
pkgrel=2
pkgdesc="X11 miscellaneous micro-utility library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext' 'lib32-libxt' $_pkgbasename)
makedepends=('xorg-util-macros' 'gcc-multilib')
license=('custom')
options=('!libtool')
source=(${url}/releases/individual/lib/libXmu-${pkgver}.tar.bz2)
sha1sums=('81876a0848070bfc4476a2538fc16898eb6d96cd')

build() {
  cd "${srcdir}/libXmu-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-static \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXmu-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
