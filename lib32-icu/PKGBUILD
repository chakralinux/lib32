# Lib32 Packages for Chakra

_pkgbasename=icu
pkgname=lib32-$_pkgbasename
pkgver=51.2
pkgrel=2
pkgdesc="International Components for Unicode library (32 bit)"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('lib32-gcc-libs' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
source=(http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz
	icu.8198.revert.icu5431.patch)
md5sums=('072e501b87065f3a0ca888f1b5165709'
         'ebd5470fc969c75e52baf4af94a9ee82')


prepare() {
  cd ${srcdir}/icu/source
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch
}

build() {
  # This version of ICU needs to set those flags 
  #http://bugs.icu-project.org/trac/ticket/7659
  export CC="gcc" 
  export CXX="g++" 
  export CFLAGS="-m32" 
  export CXXFLAGS="-m32"
 
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $srcdir/icu/source

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32

  make
}

check() {
  cd "$srcdir/icu/source"
  make -k check # passes all
}

package() {
  cd icu/source

  make -j1 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{include,sbin,share}

  # keep icu-config-32
  find "${pkgdir}/usr/bin" -type f -not -name icu-config -delete
  mv "${pkgdir}/usr/bin"/icu-config{,-32}

  install -d m644 "${pkgdir}/usr/share/licenses"
  ln -s icu "${pkgdir}/usr/share/licenses/${pkgname}"
}
