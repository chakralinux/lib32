# Lib32 Packages for Chakra

pkgname=lib32-icu
pkgver=4.6
pkgrel=1
pkgdesc="International Components for Unicode library (32 bit)"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('lib32-gcc-libs' 'icu')
makedepends=('gcc-multilib')
source=(http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz)
md5sums=('43e56b71c407be5154de681eaa646a4a')

build() {
  # This version of ICU needs to set those flags 
  #http://bugs.icu-project.org/trac/ticket/7659
  export CC="gcc" 
  export CXX="g++" 
  export CFLAGS="-m32" 
  export CXXFLAGS="-m32"
 
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $srcdir/icu/source

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32

  make
}

check() {
  cd "$srcdir/icu/source"
  make -k check # passes all
}

package() {
  cd icu/source

  make -j1 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{include,sbin,share}

  # keep icu-config-32
  find "${pkgdir}/usr/bin" -type f -not -name icu-config -delete
  mv "${pkgdir}/usr/bin"/icu-config{,-32}

  install -d m644 "${pkgdir}/usr/share/licenses"
  ln -s icu "${pkgdir}/usr/share/licenses/${pkgname}"
}
