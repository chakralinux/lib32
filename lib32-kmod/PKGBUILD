# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>

_pkgbasename=kmod
pkgname=lib32-$_pkgbasename
pkgver=12
pkgrel=1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL2')
depends=('lib32-zlib' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/kmod/kmod-$pkgver.tar.xz")
md5sums=('3d63b146c8ee5a04dfbef4be97f8226b')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/kmod-${pkgver}"

  ./configure \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-tools \
    --with-zlib \
    --with-rootprefix=/usr

  make
}

package() {
  cd "${srcdir}/kmod-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # nuke manpages and headers
  rm -rf "${pkgdir}"/usr/{share,include,bin}
}
