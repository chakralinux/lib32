# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxaw
pkgname=lib32-$_pkgbasename
pkgver=1.0.9
pkgrel=2
pkgdesc="X11 Athena Widget library (32 bit)"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxmu' 'lib32-libxpm' "$_pkgbasename>=$pkgver")
makedepends=('xorg-util-macros' 'gcc-multilib')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/lib/libXaw-${pkgver}.tar.bz2)
sha1sums=('5f8fc1b10316f31d4c6fa1d938b2297ff5bd2da5')

build() {
  cd "${srcdir}/libXaw-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libdir=/usr/lib32 \
              --disable-static CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/libXaw-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
  mkdir -p ${pkgdir}/usr/share/licenses
  ln -s ${_pkgbasename} ${pkgdir}/usr/share/licenses/${pkgname}
}
