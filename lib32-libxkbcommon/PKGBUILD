# Maintainer: Francesco Marinucci <franzmari [at] chakra-project [dot] it>

_pkgbasename=libxkbcommon
pkgname=lib32-$_pkgbasename
pkgver=0.3.2
pkgrel=1
pkgdesc='Keyboard handling library using XKB data'
arch=('x86_64')
url='http://xkbcommon.org/'
license=('custom')
depends=('lib32-glibc' $_pkgbasename=$pkgver )
makedepends=('lib32-libxcb' 'xorg-util-macros')
source=("http://xkbcommon.org/download/${pkgname#*-}-${pkgver}.tar.xz")
sha256sums=('cc378a47b01b00226ecf647b69e07df04091323846124b366cf835e5cdf5a88a')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-{docs,static,x11}
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbasename} "${pkgdir}"/usr/share/licenses/${pkgname}
}
