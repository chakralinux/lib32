# Part of X.org group
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxfixes
pkgname=lib32-$_pkgbasename
pkgver=5.0
pkgrel=2
pkgdesc="X11 miscellaneous 'fixes' extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11>=1.3.3' $_pkgbasename=$pkgver)
makedepends=('pkg-config' 'gcc-multilib' "fixesproto>=4.1.1")
options=('!libtool')
source=(${url}/releases/individual/lib/libXfixes-${pkgver}.tar.bz2)
md5sums=('678071bd7f9f7467e2fc712d81022318')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXfixes-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-static \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXfixes-${pkgver}"
  make DESTDIR="${pkgdir}" install
 
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
