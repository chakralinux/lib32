# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# contributor: (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=alsa-plugins
pkgname=lib32-$_pkgbasename
pkgver=1.1.5
pkgrel=1
pkgdesc="Extra alsa plugins (32-bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
license=(GPL)
depends=(lib32-alsa-lib $_pkgbasename=$pkgver)
makedepends=(lib32-libpulse lib32-jack lib32-speex lib32-libsamplerate lib32-gcc-libs)
optdepends=('lib32-libpulse: PulseAudio plugin'
            'lib32-jack: Jack plugin'
            'lib32-libsamplerate: libsamplerate resampling plugin'
            'lib32-speex: libspeexdsp resampling plugin')
source=("ftp://ftp.alsa-project.org/pub/plugins/$_pkgbasename-$pkgver.tar.bz2")
md5sums=('f898b0e6e8acf3f6f82bc2a0f0c84a97')

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  CFLAGS+=' -DHAVE_STDINT_H' # We have issue similar to this one https://code.google.com/p/cantata/issues/detail?id=627
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-avcodec
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/share"
}
