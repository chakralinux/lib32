# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# contributor: (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=alsa-plugins
pkgname=lib32-$_pkgbasename
pkgver=1.0.28
pkgrel=1
pkgdesc="Extra alsa plugins (32-bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
license=(GPL)
depends=(lib32-alsa-lib $_pkgbasename=$pkgver)
makedepends=(lib32-{libpulse,jack,speex,libsamplerate} gcc-multilib)
optdepends=('lib32-libpulse: PulseAudio plugin'
            'lib32-jack: Jack plugin'
            'lib32-libsamplerate: libsamplerate resampling plugin'
            'lib32-speex: libspeexdsp resampling plugin')
source=("ftp://ftp.alsa-project.org/pub/plugins/$_pkgbasename-$pkgver.tar.bz2")
md5sums=('6fcbbb31e96f8ebc5fb926184a717aa4')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-avcodec
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/share"
}
