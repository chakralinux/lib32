# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_basepkgname=llvm
pkgname='lib32-llvm'
pkgver=3.3
pkgrel=1
arch=('x86_64')
pkgdesc="Low Level Virtual Machine (32 bits version)"
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('perl' 'lib32-libffi' 'llvm' "${_basepkgname}=${pkgver}")
makedepends=('lib32-libffi' 'python2' 'gcc-multilib' 'groff')
source=("http://llvm.org/releases/${pkgver}/llvm-${pkgver}.src.tar.gz")
md5sums=('40564e1dc390f9844f1711c08b08e391')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/llvm-${pkgver}.src"

  # Fix installation directories, ./configure doesn't seem to set them right
  sed -e 's:\$(PROJ_prefix)/etc/llvm:/etc/llvm:' \
      -e 's:\$(PROJ_prefix)/lib:$(PROJ_prefix)/lib32/llvm:' \
      -e 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
      -i Makefile.config.in

  sed -e '/ActiveLibDir = ActivePrefix/s:lib:lib32/llvm:' \
      -i tools/llvm-config/llvm-config.cpp

  sed -e 's:LLVM_LIBDIR="${prefix}/lib":LLVM_LIBDIR="${prefix}/lib32/llvm":' \
      -i autoconf/configure.ac \
      -i configure

  # Fix insecure rpath (http://bugs.archlinux.org/task/14017)
  sed -e 's:$(RPATH) -Wl,$(\(ToolDir\|LibDir\|ExmplDir\))::g' \
      -i Makefile.rules

  # Apply strip option to configure
  _optimized_switch="enable"
  [[ $(check_option strip) == n ]] && _optimized_switch="disable"

  # Include location of libffi headers in CPPFLAGS
  export CPPFLAGS="${CPPFLAGS} $(pkg-config --cflags libffi)"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32/llvm \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-libffi \
    --enable-targets=all \
    --disable-expensive-checks \
    --disable-debug-runtime \
    --disable-assertions \
    --with-binutils-include=/usr/include \
    --with-cxx-include-arch="${CHOST}" \
    "--${_optimized_switch}-optimized"

  make REQUIRES_RTTI=1
}

package() {
  cd "${srcdir}/llvm-${pkgver}.src"

  # We move the clang directory out of the tree so it won't get installed and
  # then we bring it back in for the clang package
  # mv tools/clang "$srcdir"
  # -j1 is due to race conditions during the installation of the OCaml bindings
  make -j1 DESTDIR="${pkgdir}" install

  # Fix permissions of static libs
  chmod -x "${pkgdir}"/usr/lib32/llvm/*.a

  # Fix libdir in llvm-config (http://bugs.archlinux.org/task/14487)
  sed -e 's:ABS_RUN_DIR/lib:ABS_RUN_DIR/lib32/llvm:' \
      -i "${pkgdir}/usr/bin/llvm-config"
  mv "${pkgdir}/usr/bin/llvm-config" "${pkgdir}/usr/lib32/llvm-config"

  # Get rid of example Hello transformation
  rm "${pkgdir}"/usr/lib32/llvm/*LLVMHello.*

  # Symlink the gold plugin where clang expects it
  ln -s llvm/LLVMgold.so "${pkgdir}/usr/lib32/LLVMgold.so"

  # Add ld.so.conf.d entry
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo /usr/lib32/llvm >"${pkgdir}/etc/ld.so.conf.d/llvm32.conf"

  install -D -m644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm -rf "${pkgdir}/usr"/{bin,lib,include,share/{doc,man}}

  mkdir "${pkgdir}/usr/bin"
  mv "${pkgdir}/usr/lib32/llvm-config" "${pkgdir}/usr/bin/llvm-config32"
}

# vim:set ts=2 sw=2 et:
