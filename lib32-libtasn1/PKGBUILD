# maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# contributions from Arch: https://www.archlinux.org/packages/multilib/x86_64/lib32-libtasn1/

_pkgbasename=libtasn1
pkgname=lib32-${_pkgbasename}
pkgver=4.2
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS (32 bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/libtasn1/${_pkgbasename}-${pkgver}.tar.gz{,.sig})
md5sums=('414df906df421dee0a5cf7548788d153'
         'SKIP')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd ${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/"{bin,include,share}
}
