# Maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# Contributor (x86_64): AlmAck <almack@chakraos.org>

_pkgbasename=libdrm
pkgname=lib32-$_pkgbasename
pkgver=2.4.54
pkgrel=3
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-libpciaccess' 'lib32-glibc' $_pkgbasename=$pkgver)
makedepends=(gcc-multilib)
checkdepends=('lib32-cairo')
url="http://dri.freedesktop.org/"
source=("http://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2"
        'no-pthread-stubs.patch')
sha1sums=('b73d69b42800755ed9dfd8aa9a906fe941cfbb7e'
          '1602cd40f42238935a3c8d8aebd1fc0afb974379')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  patch -p1 -i "${srcdir}/no-pthread-stubs.patch"

  autoreconf --force --install
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --enable-udev \
      --enable-intel \
      --enable-radeon \
      --enable-vmwgfx-experimental-api \
      --enable-nouveau-experimental-api \
      --enable-omap-experimental-api \
      --enable-exynos-experimental-api
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

