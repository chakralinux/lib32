# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

pkgname=('lib32-udev')
pkgver=175
pkgrel=1
arch=('x86_64')
pkgdesc="The userspace dev tools (udev) (32-bit)"
url="http://www.kernel.org/pub/linux/utils/kernel/hotplug/udev.html"
license=('GPL')
options=(!makeflags !libtool)
depends=('lib32-glibc' 'lib32-glib2' 'lib32-acl' 'lib32-libusb-compat' 'udev')
makedepends=('gcc-multilib' 'coreutils' 'util-linux' 'pciutils' 'kernel26' 'gperf' 'libxslt' 'gobject-introspection')
source=(http://www.kernel.org/pub/linux/utils/kernel/hotplug/udev-$pkgver.tar.bz2)
sha256sums=('4c7937fe5a1521316ea571188745b9a00a9fdf314228cffc53a7ba9e5968b7ab')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/udev-$pkgver

  ./configure --libdir=/usr/lib32
  make
}
  
package() {
  cd $srcdir/udev-$pkgver

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{etc,include,lib,libexec,sbin,share}
  rm -rf ${pkgdir}/usr/lib32/girepository*
}
