# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=sdl_mixer
pkgname=lib32-$_pkgbasename
pkgver=1.2.10
pkgrel=2
pkgdesc="A simple multi-channel audio mixer (32-bit)"
arch=('x86_64')
url="http://www.libsdl.org/projects/SDL_mixer/"
license=('LGPL' 'GPL')
depends=("$_pkgbasename" 'lib32-sdl>=1.2.12' 'lib32-libvorbis' 'lib32-libmikmod' 'lib32-smpeg')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL_mixer-${pkgver}.tar.gz")
md5sums=('a357558552436b0b5ea0333b3e2327df')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${srcdir}/SDL_mixer-${pkgver}"

  sed -e "/CONFIG_FILE_ETC/s/\/etc\/timidity.cfg/\/etc\/timidity++\/timidity.cfg/" \
      -e "/DEFAULT_PATH/s/\/etc\/timidity/\/etc\/timidity++/" \
      -e "/DEFAULT_PATH2/s/\/usr\/local\/lib\/timidity/\/usr\/lib\/timidity/" \
      -i timidity/config.h

  ./configure \
	--prefix=/usr \
	--libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/SDL_mixer-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir/usr/include"
  cd "$pkgdir/usr/lib32/" || return 1
  ln -sf libSDL_mixer-1.2.so.10.0.0 libSDL_mixer-1.2.so.0
}
