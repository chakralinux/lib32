# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxft
pkgname=lib32-$_pkgbasename
pkgver=2.1.14
pkgrel=1
pkgdesc="FreeType-based font drawing library for X (32-bit)"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-fontconfig' 'lib32-libxrender')
makedepends=('gcc-multilib')
options=('!libtool')
source=(${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2)
md5sums=('254e62a233491e0e1251636536163e20')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/libXft-${pkgver}
  ./configure --prefix=/usr \
    --libdir=/usr/lib32 --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
