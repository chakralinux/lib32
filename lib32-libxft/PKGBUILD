# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxft
pkgname=lib32-$_pkgbasename
pkgver=2.3.1
pkgrel=1
_ubuntu_diff_version=${pkgver}-2
pkgdesc="FreeType-based font drawing library for X (32-bit)"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-fontconfig' 'lib32-libxrender' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
provides=("${pkgname}-ubuntu")
conflicts=("${pkgname}-ubuntu")
replaces=("${pkgname}-ubuntu")
source=(${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2
        "http://archive.ubuntu.com/ubuntu/pool/main/x/xft/xft_${_ubuntu_diff_version}.diff.gz")
sha1sums=('c7ec1361e3d4f095910f8c58988ab42e32f314d9'
          'f86ef3fde13301682e0473474cd55fc8caa41ec7')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/libXft-${pkgver}
  
  # apply ubuntu patches
  patch -Np1 -i ../xft_${_ubuntu_diff_version}.diff
  for _f in $(cat "$srcdir/libXft-${pkgver}/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/libXft-${pkgver}/debian/patches/$_f"
  done
  
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
