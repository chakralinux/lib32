# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libgcrypt
pkgname=lib32-$_pkgbasename
pkgver=1.5.3
pkgrel=1
pkgdesc="a general purpose crypto library based on the code used (32-bit)"
arch=(x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('lib32-libgpg-error>=1.7' $_pkgbasename=$pkgver)
makedepends=(gcc-multilib libtool-multilib)
options=('!libtool' '!emptydirs')
source=("ftp://ftp.gnupg.org/gcrypt/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2"
  #ftp://ftp.franken.de/pub/crypt/mirror/ftp.gnupg.org/gcrypt/libgcrypt/${_pkgbasename}-${pkgver}.tar.bz2
)
sha1sums=('2c6553cc17f2a1616d512d6870fe95edf6b0e26e')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  # Use 32-bit assembler
  sed 's:path="amd64":path="i586 i386":' -i mpi/config.links

  # keep static library for , needed for cryptsetup
  ./configure --prefix=/usr --disable-padlock-support --libdir=/usr/lib32 --enable-shared
  make
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
}
