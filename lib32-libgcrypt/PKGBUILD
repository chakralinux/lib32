# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libgcrypt
pkgname=lib32-$_pkgbasename
pkgver=1.5.0
pkgrel=1
pkgdesc="A general purpose crypto library based on the code used (32-bit)"
arch=(x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('lib32-libgpg-error>=1.7' $_pkgbasename=$pkgver)
makedepends=(gcc-multilib libtool-multilib)
options=(!libtool)
source=(ftp://ftp.gnupg.org/gcrypt/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)

sha1sums=('3e776d44375dc1a710560b98ae8437d5da6e32cf')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  # Use 32-bit assembler
  sed 's:path="amd64":path="i586 i386":' -i mpi/config.links

  # keep static library for , needed for cryptsetup
  ./configure --prefix=/usr \
              --disable-padlock-support \
              --libdir=/usr/lib32 \
              --enable-shared
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
}
