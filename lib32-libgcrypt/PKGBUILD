# Lib32 Packages for Chakra, part of chakraos.org
#
# maintainer (x86_64): AlmAck almack@chakraos.org


_pkgbasename=libgcrypt
pkgname=lib32-$_pkgbasename
pkgver=1.6.4
pkgrel=1
pkgdesc="A general purpose crypto library based on the code used (32-bit)"
arch=(x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('lib32-libgpg-error>=1.10' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs' 'libtool-multilib')
source=("ftp://ftp.gnupg.org/gcrypt/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2")
sha1sums=('ed52add1ce635deeb2f5c6650e52667debd4ec70')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  # Use 32-bit assembler
  sed 's:path="amd64":path="i586 i386":' -i mpi/config.links

  # keep static library for , needed for cryptsetup
  ./configure --prefix=/usr \
              --disable-padlock-support \
              --libdir=/usr/lib32 \
              --enable-shared
  make
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
}
