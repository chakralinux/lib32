# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libjpeg
pkgname=lib32-$_pkgbasename
pkgver=8.0.2
_pkgver=8b
pkgrel=2
pkgdesc="Library of JPEG support functions (32-bit)"
arch=('x86_64')
url="http://www.ijg.org/"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('libtool' 'gcc-multilib')
options=('!libtool')
source=(http://www.ijg.org/files/jpegsrc.v${_pkgver}.tar.gz)
md5sums=('e022acbc5b36cd2cb70785f5b575661e')

build() {
  cd $srcdir/jpeg-${_pkgver}

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --enable-shared --enable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd $srcdir/jpeg-${_pkgver}
  make DESTDIR="$pkgdir" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
