# Maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=libxshmfence
pkgname=lib32-${_pkgbasename}
pkgver=1.1
pkgrel=2
pkgdesc="a library that exposes a event API on top of Linux futexes"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('GPL')
depends=('glibc')
makedepends=('xorg-util-macros' 'xproto')
source=(${url}/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('dbc2db2925ca9f216fd1e9c63d0974db9f4d49aaf5877ffb606d2d8d7e58cebe')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr --disable-static \
    --libdir=/usr/lib32
  make
}

check() {
  cd $_pkgbasename-$pkgver
  make -k check
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir/" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgbasename}"
  rm "${pkgdir}/usr/include/X11/xshmfence.h"
}
