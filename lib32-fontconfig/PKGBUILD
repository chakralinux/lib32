
# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=fontconfig
pkgname=lib32-$_pkgbasename
pkgver=2.10.93
pkgrel=4
_ubuntu_diff_version=${pkgver}-0ubuntu1
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=(x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('lib32-expat>=2.0.1' 'lib32-freetype2>=2.3.11' $_pkgbasename=$pkgver 'lib32-bzip2')
makedepends=(gcc-multilib)
options=('!libtool')
provides=("${pkgname}-ubuntu")
conflicts=("${pkgname}-ubuntu")
replaces=("${pkgname}-ubuntu")
source=("http://www.fontconfig.org/release/${_pkgbasename}-${pkgver}.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_${_ubuntu_diff_version}.debian.tar.gz")
sha256sums=('381541e4d1a150dac46af1d9ad64bdc3a131d6b2707d192209b2ed26e3e64147'
            'bd02a8858786e30a70b145494168c0c99a0ad4c8606d3bbaed99f9f3d3ea690f')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-templatedir=/etc/fonts/conf.avail \
              --with-xmldir=/etc/fonts \
              --localstatedir=/var \
              --disable-static \
              --with-default-fonts=/usr/share/fonts \
              --with-add-fonts=/usr/share/fonts \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir"/{etc,var}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
