# Lib32 Packages for Chakra, part of chakra-project.org

pkgname=skype
pkgver=4.2.0.11
pkgrel=2
arch=('x86_64')
pkgdesc="P2P software for high-quality voice communication"
url="http://www.skype.com/"
license=('custom')
options=('!strip')
install=${pkgname}.install
depends=('xdg-utils' 'hicolor-icon-theme' lib32-{qt,alsa-lib,libxss,libxv,libxcursor,libpulse})
optdepends=('lib32-libcanberra: XDG sound support')
source=("http://download.skype.com/linux/${pkgname}-${pkgver}.tar.bz2"
        'PERMISSION')
sha256sums=('87d30ab2ca219c1661c6497900da8fec5a30d8383f32240dc5a48f0aca35064e'
            '20b2755151bd24ae0f759a565744faea95942e6729f9d02e83bf2b633e9306fd')

package() {
  cd ${pkgname}-${pkgver}

  # Executable
  install -D skype "${pkgdir}/usr/bin/skype"

  # Data
  mkdir -p "${pkgdir}"/usr/share/skype/{avatars,lang,sounds}
  install -m 644 avatars/* "${pkgdir}/usr/share/skype/avatars"
  install -m 644 lang/*    "${pkgdir}/usr/share/skype/lang"
  install -m 644 sounds/*  "${pkgdir}/usr/share/skype/sounds"

  # DBus Service
  install -Dm 644 skype.conf \
    "${pkgdir}/etc/dbus-1/system.d/skype.conf"

  # Icons
  for _i in 16 32 48; do
    install -Dm 644 icons/SkypeBlue_${_i}x${_i}.png \
      "${pkgdir}/usr/share/icons/hicolor/${_i}x${_i}/skype.png"
  done

  install -Dm 644 icons/SkypeBlue_48x48.png \
    "${pkgdir}/usr/share/pixmaps/skype.png"

  # Desktop file
  install -Dm 644 skype.desktop \
    "${pkgdir}/usr/share/applications/skype.desktop"

  # License
  install -Dm 644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "$srcdir/PERMISSION" \
    "${pkgdir}/usr/share/licenses/${pkgname}/PERMISSION"
}
