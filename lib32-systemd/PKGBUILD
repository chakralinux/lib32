pkgname=lib32-systemd
_pkgbasename=systemd
pkgver=228
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('lib32-libgcrypt' 'lib32-xz' 'lib32-libcap' 'lib32-acl' 'lib32-libidn' 'lib32-gcc-libs' "$_pkgbasename=$pkgver")
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-libidn' 'lib32-glib2' 'intltool' 'gperf'
             'lib32-curl' 'lib32-bzip2' 'git')
source=("git+https://github.com/systemd/systemd.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$_pkgbasename"

  ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_pkgbasename}"

  local timeservers=({0..3}.chakra.pool.ntp.org)

  ./configure \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --enable-compat-libs \
    --disable-audit \
    --disable-tests \
    --disable-ima \
    --disable-seccomp \
    --disable-pam \
    --disable-kmod \
    --disable-networkd \
    --disable-blkid \
    --disable-libiptc \
    --disable-lz4 \
    --disable-manpages \
    --without-python \
    --disable-libcryptsetup \
    --disable-microhttpd \
    --disable-qrencode \
    --disable-xkbcommon \
    --with-sysvinit-path= \
    --with-sysvrcnd-path= \
    --with-ntp-servers="${timeservers[*]}"

  make
}

package() {
  cd "${_pkgbasename}"
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s systemd "$pkgdir/usr/share/licenses/lib32-systemd"
}
