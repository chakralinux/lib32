pkgname=lib32-systemd
_pkgbasename=systemd
pkgver=212
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-dbus-core' 'lib32-libcap' 'lib32-acl' 
             'lib32-libgcrypt' 'lib32-glib2' 'lib32-xz' 'intltool' 'gperf' 'lib32-kmod')
options=('!libtool')
source=("http://www.freedesktop.org/software/$_pkgbasename/$_pkgbasename-$pkgver.tar.xz")
md5sums=('257a75fff826ff91cb1ce567091cf270')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  ./configure \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --enable-compat-libs \
    --disable-audit \
    --disable-ima \
    --disable-pam \
    --disable-kmod \
    --disable-blkid \
    --disable-kdbus \
    --disable-seccomp \
    --without-python \
    --disable-libcryptsetup \
    --with-sysvinit-path= \
    --with-sysvrcnd-path=

  make
}

package() {
  depends=('lib32-glib2' 'lib32-libgcrypt' 'lib32-xz' "$_pkgbasename=$pkgver")
  cd "${srcdir}/systemd-${pkgver}"
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
  rm -rf "${pkgdir}"/usr/lib32/libnss*
}
