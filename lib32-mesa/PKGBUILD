# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# contributor: (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

pkgbase=lib32-mesa
pkgname=('lib32-mesa' 'lib32-libgl' 'lib32-libglapi')
pkgver=10.0.3
pkgrel=1
arch=('x86_64')
makedepends=('glproto>=1.4.14' 'lib32-libdrm>=2.4.38' 'lib32-libxxf86vm>=1.1.1' 'lib32-libxdamage>=1.1.3' 
             'lib32-expat>=2.0.1' 'lib32-libx11>=1.4.3' 'lib32-libxt>=1.1.1' 'lib32-gcc-libs>=4.7' 'lib32-llvm>=3.2' 'lib32-systemd'
             'dri2proto>=2.6' 'python2' 'libxml2' 'gcc-multilib' 'imake' 'pkg-config')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=("ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2")
md5sums=('5f9f463ef08129f6762106b434910adb')

build() {
  export CFLAGS="-m32"
  export CXXFLAGS="-m32"
  export LDFLAGS="-m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # for our llvm-config for 32 bit
  export LLVM_CONFIG=/usr/bin/llvm-config32

  cd "${srcdir}/Mesa-${pkgver}" 

  autoreconf -vfi

  ./configure --enable-32-bit \
    --libdir=/usr/lib32 \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-gallium-llvm \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-egl \
    --enable-texture-float \
    --enable-xa \
    --disable-64-bit

  make
}

package_lib32-libgl() {
  depends=('lib32-libdrm>=2.4.33' 'lib32-libxxf86vm>=1.1.0' 'lib32-libxdamage>=1.1.3' 'lib32-expat>=2.0.1' 'lib32-libglapi' 'lib32-gcc-libs>=4.5' "libgl=${pkgver}")
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer (ELF32)"
  replaces=('lib32-unichrome-dri' 'lib32-mach64-dri' 'lib32-mga-dri' 'lib32-r128-dri' 'lib32-savage-dri' 'lib32-sis-dri' 'lib32-tdfx-dri')

  cd "${srcdir}/Mesa-${pkgver}" 

  install -m755 -d "${pkgdir}/usr/lib32"
  install -m755 -d "${pkgdir}/usr/lib32/xorg/modules/extensions"

  mv lib32/libGL.so* "${pkgdir}/usr/lib32/"

  cd src/mesa/drivers/dri
  make -C ${srcdir}/Mesa-${pkgver}/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install

  ln -s libglx.xorg "${pkgdir}/usr/lib32/xorg/modules/extensions/libglx.so"

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
  ln -s libgl "$pkgdir/usr/share/licenses/libgl/lib32-libgl"
}

package_lib32-libglapi() {
  depends=('glibc' "libglapi=${pkgver}")
  pkgdesc="free implementation of the GL API -- shared library. The Mesa GL API module is responsible for dispatching all the gl* functions (ELF32)"

  cd ${srcdir}/?esa-*   
  install -m755 -d "${pkgdir}/usr/lib32"
  cp -dp --no-preserve=ownership lib32/libglapi.so* "${pkgdir}/usr/lib32/"

  install -m755 -d "${pkgdir}/usr/share/licenses/libglapi"
  ln -s libglapi "${pkgdir}/usr/share/licenses/libglapi/lib32-libglapi"
}

package_lib32-mesa() {
  depends=('lib32-libglapi' 'lib32-libgl' 'lib32-libx11>=1.4.5' 'lib32-libxt>=1.1.1' 'lib32-systemd' "mesa=${pkgver}" "glu")
  pkgdesc="Mesa 3-D graphics libraries and include files (ELF32)"
  provides=('lib32-libgles' 'lib32-libegl' 'lib32-ati-dri' 'lib32-intel-dri' 'lib32-svga-dri' 'lib32-nouveau-dri')
  conflicts=('lib32-libgles' 'lib32-libegl' 'lib32-ati-dri' 'lib32-intel-dri' 'lib32-svga-dri' 'lib32-nouveau-dri' 'lib32-udev')
  replaces=('lib32-libgles' 'lib32-libegl' 'lib32-ati-dri' 'lib32-intel-dri' 'lib32-nouveau-dri' 'lib32-svga-dri')

  cd "${srcdir}/Mesa-${pkgver}" 

  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/lib32/libglapi.so"*
  rm -f "${pkgdir}/usr/lib32/libGL.so"*
  rm -f "${pkgdir}/usr/lib32/libdricore${pkgver}.so"*
  rm -f "${pkgdir}/usr/lib32/xorg/modules/dri/swrast_dri.so"*
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/etc"

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  ln -s mesa "$pkgdir/usr/share/licenses/mesa/lib32-mesa"
}
