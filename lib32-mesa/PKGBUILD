# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

pkgbase=lib32-mesa
pkgname=('lib32-mesa' 'lib32-libgl' 'lib32-libglapi' 'lib32-libgles'
         'lib32-libegl' 'lib32-ati-dri' 'lib32-intel-dri'  'lib32-svga-dri')
pkgver=8.0.4
pkgrel=2
arch=('x86_64')
makedepends=('glproto>=1.4.12' 'pkgconfig' 'lib32-libdrm>=2.4.37'
             'lib32-libxxf86vm>=1.1.0' 'lib32-libxdamage>=1.1.3'
             'lib32-expat>=2.0.1' 'lib32-libx11>=1.3.5' 'lib32-libxt>=1.0.8'
             'lib32-gcc-libs>=4.5' 'lib32-llvm>=3.0' 'dri2proto>=2.6' 'python2'
             'libxml2' 'gcc-multilib' 'imake' 'lib32-udev' 'libvdpau')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=("ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2"
        'mesa-8.0.3-llvm-3.1-fixes.patch')
md5sums=('d546f988adfdf986cff45b1efa2d8a46'
         '0082f4d17674155aa13bc611a5687788')

build() {
  export CFLAGS="-m32"
  export CXXFLAGS="-m32"
  export LDFLAGS="-m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # for our llvm-config for 32 bit
  export LLVM_CONFIG="/usr/bin/llvm-config32"

  cd "${srcdir}/Mesa-${pkgver}" 

  patch -Np1 -i "${srcdir}/mesa-8.0.3-llvm-3.1-fixes.patch"

  autoreconf -vfi
   
  ./autogen.sh --prefix=/usr \
               --libdir=/usr/lib32 \
               --with-dri-driverdir="/usr/lib32/xorg/modules/dri" \
               --with-gallium-drivers="r300,r600,svga,swrast" \
               --with-dri-drivers="i915,i965,r200,radeon,swrast" \
               --enable-gallium-llvm \
               --enable-gallium-egl \
               --with-egl-platforms=x11,drm \
               --enable-shared-glapi \
               --enable-glx-tls \
               --enable-dri \
               --enable-glx \
               --enable-osmesa \
               --enable-gles1 \
               --enable-gles2 \
               --enable-egl \
               --enable-texture-float \
               --enable-shared-dricore \
               --enable-xa \
               --enable-vdpau \
               --enable-32-bit \
               --disable-64-bit \
  || return 1

  sed -e 's,XA_CFLAGS = \(.*\)s,XA_CFLAGS = -m32 \1,' \
      -e '42s,\$(CC),$(CC) -m32,' \
      -i src/gallium/state_trackers/xa/Makefile

  make
}

package_lib32-libgl() {
depends=('lib32-libdrm>=2.4.33' 'lib32-libxxf86vm>=1.1.0'
         'lib32-libxdamage>=1.1.3' 'lib32-expat>=2.0.1' 'lib32-gcc-libs>=4.5'
         'lib32-libglapi' 'libgl')
pkgdesc="Mesa 3D graphics library and DRI software rasterizer. (ELF32)"

  cd "${srcdir}/Mesa-${pkgver}" 

  install -m755 -d "${pkgdir}/usr/lib32"
  install -m755 -d "${pkgdir}/usr/lib32/xorg/modules/extensions"

  bin/minstall lib32/libGL.so* "${pkgdir}/usr/lib32/"
  bin/minstall lib32/libglsl.so* "${pkgdir}/usr/lib32/"
  bin/minstall lib32/libdricore.so* "${pkgdir}/usr/lib32/"

  make -C ${srcdir}/Mesa-${pkgver}/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install
  ln -s libglx.xorg "${pkgdir}/usr/lib32/xorg/modules/extensions/libglx.so"

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
  ln -s libgl "$pkgdir/usr/share/licenses/libgl/lib32-libgl"
}

package_lib32-libglapi() {
depends=('glibc')
pkgdesc="free implementation of the GL API -- shared library. The Mesa GL API module is responsible for dispatching all the gl* functions"

  cd ${srcdir}/?esa-*   
  install -m755 -d "${pkgdir}/usr/lib32"
  bin/minstall lib32/libglapi.so* "${pkgdir}/usr/lib32/"

  install -m755 -d "${pkgdir}/usr/share/licenses/libglapi"
  ln -s libglapi "${pkgdir}/usr/share/licenses/libglapi/lib32-libglapi"
}

package_lib32-libgles() {
depends=('libgles')
pkgdesc="Mesa GLES libraries. (ELF32)"

  cd "${srcdir}/Mesa-${pkgver}" 

  install -m755 -d "${pkgdir}/usr/lib32"
  install -m755 -d "${pkgdir}/usr/lib32/pkgconfig"
  bin/minstall lib32/libGLESv* "${pkgdir}/usr/lib32/"
  bin/minstall src/mapi/es1api/glesv1_cm.pc "${pkgdir}/usr/lib32/pkgconfig/"
  bin/minstall src/mapi/es2api/glesv2.pc "${pkgdir}/usr/lib32/pkgconfig/"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgles"
  ln -s libgles "$pkgdir/usr/share/licenses/libgles/lib32-libgles"
}

package_lib32-libegl() {
depends=('lib32-udev' 'libegl')
pkgdesc="Mesa libEGL libraries. (ELF32)"

  cd "${srcdir}/Mesa-${pkgver}" 

  install -m755 -d "${pkgdir}/usr/lib32"
  install -m755 -d "${pkgdir}/usr/lib32/egl"
  install -m755 -d "${pkgdir}/usr/lib32/pkgconfig"
  bin/minstall lib32/libEGL.so* "${pkgdir}/usr/lib32/"
  bin/minstall lib32/egl/* "${pkgdir}/usr/lib32/egl/"
  bin/minstall src/egl/main/egl.pc "${pkgdir}/usr/lib32/pkgconfig/"

  install -m755 -d "${pkgdir}/usr/share/licenses/libegl"
  ln -s libgles "$pkgdir/usr/share/licenses/libegl/lib32-libegl"
}

package_lib32-mesa() {
depends=('lib32-libgl' 'lib32-libx11>=1.3.5' 'lib32-libxt>=1.0.8' 'mesa')
pkgdesc="Mesa 3D graphics libraries and include files. (ELF32)"

  cd "${srcdir}/Mesa-${pkgver}" 

  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/lib32/libglapi.so"*
  rm -f "${pkgdir}/usr/lib32/libGL.so"*
  rm -f "${pkgdir}/usr/lib32/libGLESv"*
  rm -f "${pkgdir}/usr/lib32/libEGL"*
  rm -rf "${pkgdir}/usr/lib32/egl"
  rm -f ${pkgdir}/usr/lib32/pkgconfig/{glesv1_cm.pc,glesv2.pc,egl.pc}
  rm -rf "$pkgdir"/{usr/include,usr/lib32/xorg}

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  ln -s mesa "$pkgdir/usr/share/licenses/mesa/lib32-mesa"
}

package_lib32-ati-dri() {
depends=("lib32-libgl=${pkgver}")
pkgdesc="Mesa DRI + Gallium3D for r300 and later chipsets drivers for AMD/ATI Radeon. (ELF32)"
conflicts=('xf86-video-ati<6.9.0-6')

  cd ${srcdir}/?esa-*/src/mesa/drivers/dri
  # classic mesa drivers for radeon,r200
  make -C radeon DESTDIR="${pkgdir}" install
  make -C r200 DESTDIR="${pkgdir}" install
  # gallium3D driver for R300 and R600 r300_dri.so/r600_dri.so
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-r600 DESTDIR="${pkgdir}" install
}

package_lib32-intel-dri() {
depends=("lib32-libgl=${pkgver}")
pkgdesc="Mesa DRI drivers for Intel. (ELF32)"

  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/i915 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/i965 DESTDIR="${pkgdir}" install
}
package_lib32-svga-dri() {
depends=("mesa=${pkgver}")
pkgdesc="Gallium3D VMware guest GL driver."

  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-vmwgfx DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
