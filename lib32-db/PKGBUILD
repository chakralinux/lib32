# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=db
pkgname=lib32-$_pkgbasename
pkgver=4.8.26
pkgrel=2
pkgdesc="The Berkeley DB embedded database system (32-bit)"
arch=('x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
depends=('lib32-gcc-libs' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool' 'force')
source=(http://download-uk.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
md5sums=('3476bac9ec0f3c40729c8a404151d5e3')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/$_pkgbasename-${pkgver}/build_unix
  ../dist/configure --prefix=/usr --libdir=/usr/lib32 --enable-compat185 \
    --enable-shared --enable-static --enable-cxx
  make LIBSO_LIBS=-lpthread
}


package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}/build_unix"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{docs,include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
