# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=util-linux-ng
pkgname=lib32-$_pkgbasename
pkgver=2.17.2
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux (32-bit)"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('x86_64')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
license=('GPL2')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.kernel.org/pub/linux/utils/${_pkgbasename}/v2.17/${_pkgbasename}-${pkgver}.tar.bz2
        fsck-static.patch)

md5sums=('4635725a3eef1c57090bac8ea5e082e6'
         '499f80605a50d1c7b27894f3319343e8')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # Provide static fsck
  patch -Np1 -i "${srcdir}/fsck-static.patch" || return 1

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c
  mkdir -p "${pkgdir}/var/lib/hwclock"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  autoreconf
  automake
  ./configure --without-ncurses --libdir=/usr/lib32

  make -C shlibs
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make -C shlibs DESTDIR="${pkgdir}" install

  # remove files
  rm -rf "${pkgdir}"/usr/{include,share}
}
