pkgname=steam
pkgver=1.0.0.53
pkgrel=3
pkgdesc="Digital distribution client bootstrap package"
arch=('x86_64')
url="http://steampowered.com/"
license=('custom')
optdepends=('lib32-flashplugin: for flash video'
            'qarma: for qt5 warning dialogs'
            'freshplayerplugin: alternative flash video support'
            'lib32-alsa-plugins: for pulseaudio on some games'
            'lib32-mesa: for open source driver users'
            'lib32-catalyst-utils: for AMD Catalyst users'
            'lib32-nvidia-utils: for NVIDIA proprietary blob users'
            'lib32-primus: for NVIDIA + Bumblebee users')
install=steam.install
source=("http://repo.steampowered.com/$pkgname/pool/$pkgname/s/$pkgname/${pkgname}_$pkgver.tar.gz"
        'alsa_sdl_audiodriver.patch')
sha256sums=('10932f2e58b0d0288e0820b8e97e6f3112409f770ecb64c3cd44e87450c1c944'
            '174a110eda1c9d5b1c92a4490b266b31918559bbf8292a94905221c92da4bc0e')


prepare() {
    patch -d "$pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
}

package() {
  depends=('bash' 'xterm' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
    'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-liberation' 'python3' 'lib32-sdl'
    'lib32-libvorbis' 'lib32-alsa-lib' 'lib32-libgl' 'lib32-libgcrypt'
    'lib32-nss' 'lib32-openal' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss'
    'lib32-libxshmfence'
  )

  make -C "$pkgname" DESTDIR="$pkgdir" install

# Install license
  install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" \
    "$pkgdir/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"

  install -Dm644 steam/lib/udev/rules.d/99-steam-controller-perms.rules \
    "$pkgdir"/usr/lib/udev/rules.d/99-steam-controller-perms.rules
}
