pkgname=steam
pkgver=1.0.0.51
pkgrel=2
pkgdesc="Digital distribution client bootstrap package"
arch=('x86_64')
url="http://steampowered.com/"
license=('custom')
optdepends=(
      'lib32-mesa-dri: for open source driver users'
      'lib32-catalyst-utils: for AMD Catalyst users'
      'lib32-nvidia-utils: for NVIDIA proprietary blob users'
      'lib32-primus: for NVIDIA + Bumblebee users'
      'lib32-flashplugin: for flash video'
      'lib32-alsa-plugins: for pulseaudio on some games')

install=steam.install  
source=("http://repo.steampowered.com/$pkgname/pool/$pkgname/s/$pkgname/${pkgname}_$pkgver.tar.gz"
        'lib32-flashplugin-path.patch'
        'alsa_sdl_audiodriver.patch'
        'steam.patch'
        'steam.sh.patch'
        '80-steam-controller-permission.rules')
sha256sums=('bf766db05fcc3a8079e7e514accfbd2503813a3c70ff8336aa93e554ed7719be'
         '87b9e9f74709a43fb8424d7bb3074fb576f72c56d78a045e3b4ed4719600b0fa'
         '174a110eda1c9d5b1c92a4490b266b31918559bbf8292a94905221c92da4bc0e'
         'ae933bda073e11ad6ae61d0ede0b472ba598df251c30690592a61c11779c7ee4'
         'cfc2f1042cc890cbd0c0e0396fb10a836cacf3cfc97124a8dc78b54696a142a8'
         'ab6efa7c0aaf408f51eee8e1d83ec07004730848bdef3ab9e091847fb915f240')


prepare() {
    patch -d "$pkgname" -Np1 -i "$srcdir/lib32-flashplugin-path.patch"
    patch -d "$pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
    
    patch -d "$pkgname" -Np1 -i "$srcdir/steam.patch"
    
    # patch steam.sh that is inside an archive
    cd $srcdir/steam
    tar -xJf bootstraplinux_ubuntu12_32.tar.xz
    cd ../
    # delete the zenity check
    sed -i s!zenity!''! $srcdir/steam/steamdeps.txt
    patch -d "$pkgname" -Np1 -i "$srcdir/steam.sh.patch"
    # create the archive again
    cd steam
    rm bootstraplinux_ubuntu12_32.tar.xz
    tar -cJf bootstraplinux_ubuntu12_32.tar.xz \
    linux32 ubuntu12_32 steam.sh steam_install_agreement.txt steamdeps.txt
}

package() {
  # Install license
  make -C "$pkgname" DESTDIR="$pkgdir" install
  
  install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" "$pkgdir/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"
  
  install -Dm644 $srcdir/80-steam-controller-permission.rules $pkgdir/usr/lib/udev/rules.d/80-steam-controller-permission.rules
}
