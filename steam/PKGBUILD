pkgname=steam
pkgver=1.0.0.54
pkgrel=2
pkgdesc="Digital distribution client bootstrap package"
arch=('x86_64')
url="http://steampowered.com/"
license=('custom')
optdepends=('lib32-flashplugin: for flash video'
            'freshplayerplugin: alternative flash video support'
            'lib32-alsa-plugins: for pulseaudio on some games'
            'lib32-mesa: for open source driver users'
            'lib32-catalyst-utils: for AMD Catalyst users'
            'lib32-nvidia-utils: for NVIDIA proprietary blob users'
            'lib32-primus: for NVIDIA + Bumblebee users')
            
install=steam.install  
source=("http://repo.steampowered.com/$pkgname/pool/$pkgname/s/$pkgname/${pkgname}_$pkgver.tar.gz"
        'alsa_sdl_audiodriver.patch'
        'steam_launcher.sh')
sha256sums=('42531bf2615afed54c333ae9afeebe8cb68a31304ddfcf90e61b3532a5597386'
            '174a110eda1c9d5b1c92a4490b266b31918559bbf8292a94905221c92da4bc0e'
            '9b54b38abd3b8b449a445069d21f042b542ca5c4edd82908b5f68c7377dd7f19')


prepare() {
    cd ${pkgname}
    patch -Np1 < "${srcdir}/alsa_sdl_audiodriver.patch"

    # apply roundups for udev rules
    sed -r 's|("0666")|"0660", TAG+="uaccess"|g' -i lib/udev/rules.d/99-steam-controller-perms.rules
    sed -r 's|("misc")|\1, OPTIONS+="static_node=uinput"|g' -i lib/udev/rules.d/99-steam-controller-perms.rules
    sed -r 's|(, TAG\+="uaccess")|, MODE="0660"\1|g' -i lib/udev/rules.d/60-HTC-Vive-perms.rules
}

package() {
  depends=(
  'bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
  'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-liberation'
  'python3' 'lib32-sdl' 'lib32-libvorbis' 'lib32-alsa-lib' 'lib32-libgl'
  'lib32-libgcrypt' 'lib32-nss' 'lib32-openal' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss' 'lib32-libxshmfence' 'zenity')

  make -C "$pkgname" DESTDIR="$pkgdir" install
  
  # Install license
  install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" "$pkgdir/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"
  
  install -Dm644 $srcdir/steam/lib/udev/rules.d/99-steam-controller-perms.rules $pkgdir/usr/lib/udev/rules.d/99-steam-controller-perms.rules
  install -Dm644 $srcdir/steam/lib/udev/rules.d/60-HTC-Vive-perms.rules $pkgdir/usr/lib/udev/rules.d/70-htc-vive.rules
  
  install -Dm 644 $srcdir/steam/debian/changelog -t "${pkgdir}/usr/share/doc/${pkgname}"

  # window that points the user to the Steam's wiki page
  # and also add an hack to load chakra libs instead of steam ones
  install -D steam_launcher.sh $pkgdir/usr/bin/steam_launcher.sh
  sed -i s!"Exec=/usr/bin/steam %U"!"Exec=/usr/bin/steam_launcher.sh"! \
  $pkgdir/usr/share/applications/steam.desktop
}
