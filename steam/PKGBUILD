pkgname=steam
pkgver=1.0.0.51
pkgrel=6
pkgdesc="Digital distribution client bootstrap package"
arch=('x86_64')
url="http://steampowered.com/"
license=('custom')
optdepends=('lib32-flashplugin: for flash video'
            'freshplayerplugin: alternative flash video support'
            'lib32-alsa-plugins: for pulseaudio on some games'
            'lib32-mesa: for open source driver users'
            'lib32-catalyst-utils: for AMD Catalyst users'
            'lib32-nvidia-utils: for NVIDIA proprietary blob users'
            'lib32-primus: for NVIDIA + Bumblebee users')
            
install=steam.install  
source=("http://repo.steampowered.com/$pkgname/pool/$pkgname/s/$pkgname/${pkgname}_$pkgver.tar.gz"
        'alsa_sdl_audiodriver.patch'
        'steam.patch'
        'steam.sh.patch'
        'steam_info.sh')
sha256sums=('bf766db05fcc3a8079e7e514accfbd2503813a3c70ff8336aa93e554ed7719be'
         '174a110eda1c9d5b1c92a4490b266b31918559bbf8292a94905221c92da4bc0e'
         'ae933bda073e11ad6ae61d0ede0b472ba598df251c30690592a61c11779c7ee4'
         '7d33435937e553a6cb1e6918d1024d96c6081e8df560ea5bd1252146cfe670a8'
         '9b54b38abd3b8b449a445069d21f042b542ca5c4edd82908b5f68c7377dd7f19')


prepare() {
    patch -d "$pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
    install_agreement_window
}

install_agreement_window() {
    ##patches for to be able to approve the 'install agreement' other than through the terminal
    patch -d "$pkgname" -Np1 -i "$srcdir/steam.patch"
    
    # patch steam.sh that is inside an archive
    cd $srcdir/steam
    tar -xJf bootstraplinux_ubuntu12_32.tar.xz
    cd ../
    # delete the zenity check
    sed -i s!zenity!''! $srcdir/steam/steamdeps.txt
    patch -d "$pkgname" -Np1 -i "$srcdir/steam.sh.patch"
    # create the archive again
    cd steam
    rm bootstraplinux_ubuntu12_32.tar.xz
    tar -cJf bootstraplinux_ubuntu12_32.tar.xz \
    linux32 ubuntu12_32 steam.sh steam_install_agreement.txt steamdeps.txt

}

package() {
  depends=(
  'bash' 'xterm' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
  'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-font' 'kde-baseapps-kdialog'
  'python3' 'lib32-sdl' 'lib32-libvorbis' 'lib32-alsa-lib' 'lib32-libgl'
  'lib32-libgcrypt' 'lib32-nss' 'lib32-openal' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss' 'lib32-libxshmfence')

  make -C "$pkgname" DESTDIR="$pkgdir" install
  
# Install license
  install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" "$pkgdir/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"
  
  install -Dm644 $srcdir/steam/lib/udev/rules.d/99-steam-controller-perms.rules $pkgdir/usr/lib/udev/rules.d/99-steam-controller-perms.rules
  
  # window that points the user to the Steam's wiki page
  install -D steam_info.sh $pkgdir/usr/bin/steam_info.sh
  sed -i s!"Exec=/usr/bin/steam %U"!"Exec=/usr/bin/steam_info.sh"! \
  $pkgdir/usr/share/applications/steam.desktop
}
