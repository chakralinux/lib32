pkgname=steam
pkgver=1.0.0.50
pkgrel=1
pkgdesc="Digital distribution client bootstrap package"
arch=('x86_64')
url="http://steampowered.com/"
license=('custom')
depends=('bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
      'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-liberation' 'python'
      'lib32-sdl' 'lib32-libvorbis' 'lib32-alsa-lib' 'lib32-libgl'
      'lib32-libgcrypt' 'lib32-nss' 'lib32-openal'
      'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxshmfence')
optdepends=(
      'lib32-mesa-dri: for open source driver users'
      'lib32-catalyst-utils: for AMD Catalyst users'
      'lib32-nvidia-utils: for NVIDIA proprietary blob users'
      'lib32-primus: for NVIDIA + Bumblebee users'
      'lib32-flashplugin: for flash video'
      'lib32-alsa-plugins: for pulseaudio on some games')

install=steam.install  
source=("http://repo.steampowered.com/$pkgname/pool/$pkgname/s/$pkgname/${pkgname}_$pkgver.tar.gz"
        'lib32-flashplugin-path.patch'
        'alsa_sdl_audiodriver.patch')
md5sums=('2fab57a606705012a4f800a28670dc3a'
         '1d83a1df55d677f35ce415e0750f4448'
         'ac0e03d70f1101331598b2b32ed9bac3')

prepare() {
    patch -d "$pkgname" -Np1 -i "$srcdir/lib32-flashplugin-path.patch"
    patch -d "$pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
}

package() {
  # Install license
  make -C "$pkgname" DESTDIR="$pkgdir" install
  
  install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" "$pkgdir/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"
}
