_pkgbasename=libgusb
pkgname=lib32-$_pkgbasename
pkgver=0.2.6
pkgrel=1
pkgdesc="GLib wrapper around libusb1 (32 bit)"
arch=(x86_64)
url="https://gitorious.org/gusb/"
license=(LGPL2.1)
depends=('lib32-glib2' 'lib32-systemd' 'lib32-libusb' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib' 'gobject-introspection')
source=(http://people.freedesktop.org/~hughsient/releases/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('6f638bdbc4e77643c0a198a300e50b592ad326e56bddf359de15e04689209c06')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  make -C $_pkgbasename-$pkgver DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}
