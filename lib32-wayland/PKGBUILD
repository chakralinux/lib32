# Maintainer: Francesco Marinucci <franzmari [at] chakra-project [dot] it>

_pkgbasename=wayland
pkgname=lib32-$_pkgbasename
pkgver=1.14.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-glibc' 'lib32-libffi' 'lib32-expat' 'lib32-libxml2' $_pkgbasename=$pkgver )
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611') # Bryce Harrington
source=("http://wayland.freedesktop.org/releases/wayland-${pkgver}.tar.xz"{,.sig})
sha256sums=('ed80cabc0961a759a42092e2c39aabfc1ec9a13c86c98bbe2b812f008da27ab8'
            'SKIP')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-documentation \
    --disable-static
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbasename} "${pkgdir}"/usr/share/licenses/${pkgname}
}
