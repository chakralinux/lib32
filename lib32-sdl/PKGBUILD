# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=sdl
pkgname=lib32-$_pkgbasename
pkgver=1.2.14
pkgrel=2
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (32-bit)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('LGPL')
depends=('lib32-libxext' 'lib32-libxrender' 'lib32-libx11' $_pkgbasename)
makedepends=('lib32-alsa-lib' 'lib32-mesa' 'lib32-libpulse' gcc-multilib)
options=('!libtool')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
        sdl-1.2.14-joystick-crash.diff
        sdl-1.2.14-fix-mouse-clicking.patch
        sdl-1.2.14-fix-disappearing-cursor.patch)
md5sums=('e52086d1b508fa0b76c52ee30b55bec4'
         '9d8890b3817736a5d365f7497f096634'
         '04d8c179f125e04bcd4c9d60e013c2d7'
         'a6cf3e71b653aa97d0d8ae6c0a789807')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/SDL-${pkgver}
  patch -Np1 -i $srcdir/sdl-1.2.14-joystick-crash.diff
  patch -Np1 -i $srcdir/sdl-1.2.14-fix-mouse-clicking.patch
  patch -Np1 -i $srcdir/sdl-1.2.14-fix-disappearing-cursor.patch
  ./configure --prefix=/usr --disable-nasm --enable-alsa \
              --with-x --disable-rpath --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/SDL-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
