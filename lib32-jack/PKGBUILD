# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=jack
pkgname=lib32-$_pkgbasename
_longname=jack-audio-connection-kit
pkgver=0.118.0
pkgrel=1
pkgdesc="A low-latency audio server (32-bit)"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
url="http://jackaudio.org/"
options=('!libtool')
source=(http://jackaudio.org/downloads/${_longname}-${pkgver}.tar.gz)
md5sums=('d58e29a55f285d54e75134cec8e02a10')

build() {
  cd ${srcdir}/${_longname}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make -C libjack
}

package() {
  cd ${srcdir}/${_longname}-${pkgver}

  make DESTDIR=${pkgdir} install-pkgconfigDATA
  make -C libjack DESTDIR=${pkgdir} install
}

