# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxi
pkgname=lib32-$_pkgbasename
pkgver=1.6.1
pkgrel=1
pkgdesc="X11 Input extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org"
depends=('lib32-libxext' 'inputproto' $_pkgbasename)
makedepends=('pkgconfig' 'xorg-util-macros' gcc-multilib)
options=(!libtool)
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha1sums=('4b53b41fdaa3acc86606c696c68d5eed11454612')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXi-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-static \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXi-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
