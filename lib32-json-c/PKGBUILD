#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=lib32-json-c
_basepkgname=json-c
pkgver=0.9
pkgrel=1
pkgdesc="JSON-C implements a reference counting object model that allows you to easily construct JSON objects in C, output them as JSON formatted strings and parse JSON formatted strings back into the C representation of JSON objects."
arch=('i686' 'x86_64')
url="http://oss.metaparadigm.com/json-c"
depends=($_basepkgname)
license=('GPL')
source=("http://oss.metaparadigm.com/${_basepkgname}/${_basepkgname}-${pkgver}.tar.gz")
md5sums=('3a13d264528dcbaf3931b0cede24abae')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cd "${srcdir}/${_basepkgname}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_basepkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}

# vim:set ts=2 sw=2 et:
