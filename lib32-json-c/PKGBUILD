#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Daniele Cocca <jmc@chakra-project.org>

_pkgbasename=json-c
pkgname=lib32-$_pkgbasename
pkgver=0.11
pkgrel=2
pkgdesc="JSON-C implements a reference counting object model that allows you to easily construct JSON objects in C, output them as JSON formatted strings and parse JSON formatted strings back into the C representation of JSON objects."
arch=('x86_64')
url="https://github.com/json-c/json-c/wiki"
depends=($_pkgbasename=$pkgver)
license=('GPL')
options=(!libtool !makeflags)
source=("https://s3.amazonaws.com/${_pkgbasename}_releases/releases/${_pkgbasename}-${pkgver}.tar.gz")
md5sums=('aa02367d2f7a830bf1e3376f77881e98')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}

# vim:set ts=2 sw=2 et:
