# Lib32 Packages for Chakra, part of chakra-project.org
#

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.14.10
pkgrel=1
_ubuntu_diff_version=${pkgver}-1
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
makedepends=('gcc-multilib' 'libtool-multilib')
depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender' 'lib32-fontconfig'
         'lib32-pixman' 'lib32-glib2' 'lib32-mesa' 'lib32-libgl'
         ${_pkgbasename}=${pkgver})
provides=("${pkgname}-ubuntu")
conflicts=("${pkgname}-ubuntu")
replaces=("${pkgname}-ubuntu")
source=("http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_${_ubuntu_diff_version}ubuntu1.debian.tar.xz")
sha1sums=('28c59d85d6b790c21b8b59ece73a6a1dda28d69a'
          '89429dcce2a2efe930361b98f6d8210b885bba93')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  autoreconf -vfi
  ./configure --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --enable-tee \
      --enable-gl \
      --enable-egl
      # --disable-xlib-xcb
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
