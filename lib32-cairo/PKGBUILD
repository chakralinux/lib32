# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.12.14
pkgrel=2
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
depends=('lib32-libpng' 'lib32-libxrender' 'lib32-fontconfig' 'lib32-libxext'
         'lib32-pixman' 'lib32-glib2' ${_pkgbasename}=${pkgver})
makedepends=('gcc-multilib' 'libtool-multilib')
source=(http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz
        cairo-1.10.0-buggy_gradients.patch)
sha1sums=('9106ab09b2e7b9f90521b18dd4a7e9577eba6c15'
          'e867f1020391686b972d5bedcce3c6d1193d7953')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -p1 -i "${srcdir}/cairo-1.10.0-buggy_gradients.patch"

  autoreconf -vfi
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --enable-tee \
      --disable-xlib-xcb
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
