# Lib32 Packages for Chakra, part of chakra-project.org
#

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.12.16
pkgrel=3
_ubuntu_diff_version=${pkgver}-0ubuntu2
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
depends=('lib32-libpng' 'lib32-libxrender' 'lib32-fontconfig' 'lib32-libxext'
         'lib32-pixman' 'lib32-glib2' ${_pkgbasename}=${pkgver})
makedepends=('gcc-multilib' 'libtool-multilib')
provides=("${pkgname}-ubuntu")
conflicts=("${pkgname}-ubuntu")
replaces=("${pkgname}-ubuntu")
source=("http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_${_ubuntu_diff_version}.debian.tar.gz")
sha1sums=('4f6e337d5d3edd7ea79d1426f575331552b003ec'
          '95d873fda436a1cc29cda1310f155ef0e3258c05')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  autoreconf -vfi
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --enable-tee \
              --disable-xlib-xcb
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
