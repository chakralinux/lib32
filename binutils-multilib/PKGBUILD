# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer: Anke Boersma <abveritas[at]chakra-project[dot]org>
# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer: Manuel Tortosa <manutortosa@chakra-project.org>

# toolchain build order: 
# lib32-glibc > binutils-multilib > gcc-multilib > binutils-multilib > lib32-glibc > gcc-multilib

_pkgname=binutils
pkgname=binutils-multilib
pkgver=2.22
pkgrel=1
_date=20120109
pkgdesc="A set of programs to assemble and manipulate binary and object files for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('multilib-devel')
provides=("binutils=$pkgver-$pkgrel")
conflicts=('binutils')
depends=('glibc>=2.14' 'zlib')
makedepends=('dejagnu' 'gcc-multilib') # Make sure we compile this with gcc-multilib
options=('!libtool' '!distcc' '!ccache')
install=binutils.install
source=(http://chakra-linux.org/sources/${_pkgname}/${_pkgname}-${pkgver}_${_date}.tar.bz2)
md5sums=('d6df201cbf66394defc603e1318061fd')

build() {
  cd ${srcdir}
  mkdir binutils-build && cd binutils-build

  ${srcdir}/binutils/configure --prefix=/usr \
    --enable-ld=default --enable-gold \
    --enable-plugins --enable-threads \
    --enable-shared \
    --enable-64-bit-bfd --enable-multilib

  # check the host environment and makes sure all the necessary tools are available
  make configure-host

  make tooldir=${pkgdir}/usr
}

check() {
  cd ${srcdir}/binutils-build

  # do not abort on errors - manually check log files
  make -k -j1 check || true
}

package() {
  cd ${srcdir}/binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
  
  # Add some useful headers
  install -m644 ${srcdir}/binutils/include/libiberty.h ${pkgdir}/usr/include
  install -m644 ${srcdir}/binutils/include/demangle.h ${pkgdir}/usr/include

  # Rebuild libiberty.a with -fPIC
  make -C libiberty clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty
  install -m644 libiberty/libiberty.a ${pkgdir}/usr/lib

  # Rebuild libbfd.a with -fPIC
  make -C bfd clean
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd
  install -m644 bfd/libbfd.a ${pkgdir}/usr/lib

  # Remove Windows/Novell specific man pages
  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >${pkgdir}/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >${pkgdir}/usr/lib/libopcodes.so
}
