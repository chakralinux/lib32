# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=lib32-dbus-core
_pkgbasename=dbus-core
pkgver=1.4.14
pkgrel=1
pkgdesc="Freedesktop.org message bus system (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/Software/dbus"
license=('GPL' 'custom')
depends=('lib32-glibc' 'lib32-expat' 'dbus-core')
makedepends=('gcc-multilib')
options=(!libtool)
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz)
md5sums=('ae6de2562a57516cfabaf56903375ba9')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/dbus-${pkgver}"

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libdir=/usr/lib32 \
                --libexecdir=/usr/lib/dbus-1.0 \
                --with-dbus-user=81 \
                --with-system-pid-file=/var/run/dbus.pid \
                --enable-inotify \
                --disable-dnotify \
                --disable-verbose-mode \
                --disable-static \
                --disable-tests \
                --disable-asserts \
                --without-x
    make
}
package() {
    cd "${srcdir}/dbus-${pkgver}"
    make DESTDIR=${pkgdir} install

    rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
    rm -rf "${pkgdir}"/{etc,var}

    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
