# Contribution from Arch:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Kitching <chriskitching@linux.com>

pkgname=lib32-gstreamer
pkgver=1.12.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('lib32-libxml2' 'lib32-glib2' 'gstreamer' 'lib32-libunwind')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection' 'python2')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz{,.asc})
sha256sums=('d388f492440897f02b01eebb033ca2d41078a3d85c0eddc030cdea5a337a216e'
            'SKIP')
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure\
    --build=i686-pc-linux-gnu\
    --prefix=/usr\
    --sysconfdir=/etc\
    --localstatedir=/var\
    --libexecdir=/usr/lib32\
    --libdir=/usr/lib32\
    --disable-maintainer-mode\
    --disable-debug\
    --disable-examples\
    --with-package-name="GStreamer (Chakra GNU/Linux)" \
    --with-package-origin="https://chakralinux.org" \
    --disable-static\
    --disable-docbook\
    --disable-gtk-doc-html\
    --disable-gtk-doc-pdf\
    --disable-gtk-doc\
    --disable-benchmarks

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -R "${pkgdir}"/usr/{share,include,bin}
}
