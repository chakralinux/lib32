# Ported from CCR: kote <koteccr at gmail dot com>

_pkgbasename=gtk3
pkgname=lib32-$_pkgbasename
pkgver=3.10.7
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gtk.org/"
depends=('lib32-atk' 'lib32-gdk-pixbuf2' 'lib32-libxcomposite'
	 'lib32-libcups' 'lib32-libxcursor' 'lib32-libxdamage'
	 'lib32-libxi' 'lib32-libxinerama' 'lib32-libxrandr'
	 'lib32-pango' 'lib32-cairo' 'lib32-keyutils' 
	 'lib32-krb5' 'lib32-e2fsprogs' 'lib32-at-spi2-atk'
	 'lib32-colord' ${_pkgbasename}=${pkgver} 'lib32-libxkbcommon' 'lib32-wayland')
makedepends=('gcc-multilib')
options=('!libtool' '!docs')
install=gtk3.install
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz")
sha256sums=('b7e9de15385031cff43897e7e59f6692eaabf500f36eef80e6b9d6486ad49427')

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "gtk+-${pkgver}"
  CXX=/bin/false ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-gtk2-dependency \
      --disable-schemas-compile \
      --enable-introspection=no \
      --libdir=/usr/lib32
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool  
  make
}

package() {
  cd "gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  rm "${pkgdir}"/usr/bin/gtk3-{demo-application,widget-factory}
  rm -rf "${pkgdir}"/usr/{include,share,bin/gtk3-demo} "${pkgdir}/etc"
}
