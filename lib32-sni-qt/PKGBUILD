#
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alcasa Mz <alcasa.mz@gmail.com>

pkgname=lib32-sni-qt
_pkgname=sni-qt
pkgver=0.2.6
pkgrel=3
pkgdesc='Qt4 plugin which turns all QSystemTrayIcon into StatusNotifierItems (appindicators)'
arch=('i686' 'x86_64')
url='https://launchpad.net/sni-qt'
license=('LGPL3')
depends=('lib32-libdbusmenu-qt' "${_pkgname}=${pkgver}")
makedepends=('cmake')
source=("http://launchpad.net/${_pkgname}/trunk/${pkgver}/+download/${_pkgname}-${pkgver}.tar.bz2" "qconfig.h")
md5sums=('e84c66e776f7c22c961817387f618c81'
         '8bffe79f81780c300a9a55264badb111')

prepare() {
  mkdir -p build

  # Disable building tests
  sed -i '/tests/ d' ${_pkgname}-${pkgver}/CMakeLists.txt

  mkdir -p ${_pkgname}-${pkgver}/src/QtCore
  cp ../${source[1]} ${_pkgname}-${pkgver}/src/QtCore
}

build() {
  cd build
  export CC="gcc -m32"
  export CXX="g++ -m32 "
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQT_PLUGINS_DIR=/usr/lib32/qt/plugins
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
