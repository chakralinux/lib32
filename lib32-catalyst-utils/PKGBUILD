# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgsourcename=catalyst-utils
pkgname=lib32-$_pkgsourcename
pkgver=11.11
pkgrel=1
pkgdesc="AMD/ATI catalyst driver utilities and libraries. (32-bit)"
url="http://www.ati.amd.com"
arch=(x86_64)
license=('custom')
depends=('lib32-libxext' 'lib32-libdrm' "catalyst-utils>=${pkgver}")
conflicts=('lib32-libgl' 'lib32-nvidia-utils' 'lib32-ati-dri')
replaces=('lib32-ati-fglrx-utils' 'lib32-fglrx-utils' 'lib32-ati-dri')
provides=('lib32-libgl')
source=(http://www2.ati.com/drivers/linux/ati-driver-installer-${pkgver/./-}-x86.x86_64.run
		lib32-catalyst.sh)
md5sums=('7196077aacfbaf7234a705f5352a1111'
         'af7fb8ee4fc96fd54c5b483e33dc71c4')

build() {
  # Extract Catalyst driver installer
  /bin/sh ./ati-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files
}

package() {
  cd ${srcdir}
  install -D -m755 lib32-catalyst.sh ${pkgdir}/etc/profile.d/lib32-catalyst.sh || return 1

  # Install lib32 libraries
  cd "${srcdir}/archive_files/arch/x86/usr" || return 1
  install -dm755 "${pkgdir}/usr/lib32" || return 1
  install -dm755 "${pkgdir}/usr/lib32/fglrx" || return 1                                   # since 11.4
  install -dm755 "${pkgdir}/usr/lib32/xorg/modules/dri" || return 1
  install -m755 lib/*.so* "${pkgdir}/usr/lib32/" || return 1

  install -m755 X11R6/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx" || return 1 # since 11.5
  ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx/libGL.so.1.2"      # since 11.4
  ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx-libGL.so.1.2"      # since 11.4
  ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so.1.2"            # since 11.4
  ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so.1"              # since 11.4
  ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so"                # since 11.4
  install -m755 X11R6/lib/libAMDXvBA.so.1.0 "${pkgdir}/usr/lib32/" || return 1             # since 11.4
  install -m755 X11R6/lib/libatiadlxx.so "${pkgdir}/usr/lib32/" || return 1                # since 11.4
  install -m755 X11R6/lib/libfglrx_dm.so.1.0 "${pkgdir}/usr/lib32/" || return 1            # since 11.4
  install -m755 X11R6/lib/libXvBAW.so.1.0 "${pkgdir}/usr/lib32/" || return 1               # since 11.4

  install -m755 X11R6/lib/modules/dri/*.so "${pkgdir}/usr/lib32/xorg/modules/dri/" || return 1

  cd "$pkgdir/usr/lib32/" || return 1
  ln -sf libfglrx_dm.so.1.0 libfglrx_dm.so.1 || return 1
  ln -sf libAMDXvBA.so.1.0 libAMDXvBA.so.1 || return 1
  ln -sf libXvBAW.so.1.0 libXvBAW.so.1 || return 1
  ln -sf libatiuki.so.1.0 libatiuki.so.1 || return 1
  ln -sf libatiuki.so.1.0 libatiuki.so || return 1
  install -dm755 "${pkgdir}/usr/lib32/dri" || return 1
  ln -sf xorg/modules/dri/fglrx_dri.so dri/fglrx_dri.so || return 1
}
