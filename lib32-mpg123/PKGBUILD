# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=mpg123
pkgname="lib32-${_pkgbasename}"
pkgver=1.16.0
pkgrel=2
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3. (ELF32)"
arch=('x86_64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2.1')
depends=('lib32-glibc' 'lib32-libtool' 'lib32-alsa-lib' "${_pkgbasename}=${pkgver}")
makedepends=('lib32-sdl' gcc-multilib libtool-multilib)
optdepends=('lib32-sdl: for additional audio support')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2")
sha256sums=('f00f72e385b522b8f05c9b1ed371abad438362620e6eb8164e2a99b79bb3f6d3')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure --prefix=/usr --with-audio="alsa sdl" --with-cpu=i586 --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgbasename}" "${pkgdir}/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
