# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libpulse
pkgname=lib32-$_pkgbasename
pkgdesc="A featureful, general-purpose sound server (32-bit client libraries)"
pkgver=1.0
pkgrel=1
arch=(x86_64)
url="http://pulseaudio.org/"
license=(GPL LGPL)
depends=(lib32-{dbus-core,xcb-util,json-c}
         lib32-lib{asyncns,cap,xtst,sm,sndfile,samplerate}
         $_pkgbasename)
makedepends=(pkgconfig gcc-multilib lib32-libtool intltool lib32-glib2 lib32-speex)
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=$pkgver")
conflicts=(lib32-pulseaudio)
replaces=(lib32-pulseaudio)
options=(!emptydirs !libtool !makeflags)
source=("http://freedesktop.org/software/pulseaudio/releases/pulseaudio-$pkgver.tar.xz")
md5sums=('538e0b55ab9ba2987a88047ae332f5f4')

_libs="libpulsecommon-$pkgver.la libpulse.la libpulse-simple.la libpulse-mainloop-glib.la libpulsedsp.la"

build() {
  cd "$srcdir/pulseaudio-$pkgver"

  # Don't break circular dependency between libpulse and libpulsecommon
  LDFLAGS="${LDFLAGS//-Wl,--as-needed}"


  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-hal \
    --disable-rpath \
    --disable-default-build-tests

  make -C src $_libs
}

package() {
  cd "$srcdir/pulseaudio-$pkgver"
  make -C src lib_LTLIBRARIES="$_libs" DESTDIR="$pkgdir" install-libLTLIBRARIES
  make DESTDIR="$pkgdir" install-pkgconfigDATA
}
