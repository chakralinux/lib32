# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>
# contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

_pkgbasename=libpulse
pkgname="lib32-${_pkgbasename}"
pkgdesc="A featureful, general-purpose sound server. (32-bit client libraries)"
pkgver=2.0
pkgrel=1
arch=('x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=(lib32-{dbus-core,xcb-util,json-c}
         lib32-lib{asyncns,cap,xtst,sm,sndfile,samplerate}
         "${_pkgbasename}")
makedepends=('pkgconfig' 'gcc-multilib' 'lib32-libtool' 'intltool'
		         'lib32-glib2' 'lib32-speex')
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=${pkgver}")
conflicts=('lib32-pulseaudio')
replaces=('lib32-pulseaudio')
options=(!emptydirs !libtool)
source=("http://freedesktop.org/software/pulseaudio/releases/pulseaudio-${pkgver}.tar.xz")
md5sums=('9bbde657c353fe675c3b693054175a8e')

_libs="libpulse.la libpulse-simple.la libpulse-mainloop-glib.la"
_pkglibs="libpulsecommon-$pkgver.la libpulsedsp.la"


build() {
  cd "$srcdir/pulseaudio-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-hal \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -C src $_libs $_pkglibs
}

package() {
  cd "$srcdir/pulseaudio-$pkgver"
  make -j1 -C src DESTDIR="$pkgdir" \
    lib_LTLIBRARIES="$_libs" pkglib_LTLIBRARIES="$_pkglibs" \
    install-libLTLIBRARIES install-pkglibLTLIBRARIES
  make -j1 DESTDIR="$pkgdir" install-pkgconfigDATA
}