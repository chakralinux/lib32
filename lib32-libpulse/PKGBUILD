# maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

_pkgbasename=pulseaudio
pkgname=lib32-libpulse
pkgdesc="A featureful, general-purpose sound server. (32-bit client libraries)"
pkgver=8.0
pkgrel=1
arch=(x86_64)
url="http://www.freedesktop.org/wiki/Software/PulseAudio"
license=(LGPL)
depends=(lib32-libdbus lib32-json-c lib32-systemd
         lib32-libasyncns lib32-libcap lib32-libxtst lib32-libsm lib32-libsndfile
         $_pkgbasename)
makedepends=(gcc-multilib libtool-multilib intltool lib32-glib2 lib32-speexdsp git)
optdepends=('lib32-alsa-plugins: ALSA support')
options=(!emptydirs !libtool)
source=("http://freedesktop.org/software/pulseaudio/releases/pulseaudio-$pkgver.tar.xz")
sha256sums=('690eefe28633466cfd1ab9d85ebfa9376f6b622deec6bfee5091ac9737cd1989')

_pulsever=$pkgver
[[ $_pulsever = *.*.* ]] && _pulsever=${_pulsever%.*}

_libs="libpulse.la libpulse-simple.la libpulse-mainloop-glib.la"
_pkglibs="libpulsecommon-${_pulsever}.la libpulsedsp.la"

prepare() {
  cd pulseaudio-$pkgver
}

build() {
  cd pulseaudio-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-hal \
    --disable-tcpwrap \
    --disable-bluez4 \
    --disable-samplerate \
    --disable-rpath \
    --disable-default-build-tests \
    DATADIRNAME=share

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -C src $_libs $_pkglibs
}

package() {
  cd pulseaudio-$pkgver
  make -j1 -C src DESTDIR="$pkgdir" \
    lib_LTLIBRARIES="$_libs" install-libLTLIBRARIES
  make -j1 -C src DESTDIR="$pkgdir" \
    pkglib_LTLIBRARIES="$_pkglibs" install-pkglibLTLIBRARIES
  make -j1 DESTDIR="$pkgdir" install-pkgconfigDATA
}
