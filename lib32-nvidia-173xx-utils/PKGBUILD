# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from AUR file: https://aur.archlinux.org/packages/lib32-nvidia-utils-304xx/

_pkgbasename=nvidia-173xx-utils
pkgname=lib32-$_pkgbasename
pkgver=173.14.39
pkgrel=1
pkgdesc="NVIDIA drivers utilities and libraries. (32-bit)"
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('lib32-libxvmc' 'lib32-zlib' 'lib32-gcc-libs' "$_pkgbasename=$pkgver")
conflicts=('lib32-libgl')
provides=('lib32-libgl')
license=('custom')
options=('!strip')

_parch='x86'
_pkg="NVIDIA-Linux-${_parch}-${pkgver}-pkg0"
source=("http://us.download.nvidia.com/XFree86/Linux-${_parch}/${pkgver}/NVIDIA-Linux-${_parch}-${pkgver}-pkg0.run")
md5sums=('5b423543428554ef33a200fbbe3cb9fc')

prepare() {
    cd "${srcdir}"
    [[ -d ${_pkg} ]] && rm -rf ${_pkg}
    sh ${_pkg}.run --extract-only
}

package() {
    cd "${srcdir}/${_pkg}/usr/lib"

    # OpenGL library
    install -D -m755 libGL.so.${pkgver} "${pkgdir}/usr/lib32/libGL.so.${pkgver}"
    # OpenGL core library
    install -D -m755 libGLcore.so.${pkgver} "${pkgdir}/usr/lib32/libGLcore.so.${pkgver}"

    # CUDA
    install -D -m755 libcuda.so.${pkgver} "${pkgdir}/usr/lib32/libcuda.so.${pkgver}"

    # nvidia-tls library
    install -D -m755 tls/libnvidia-tls.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-tls.so.${pkgver}"

    install -D -m755 libnvidia-cfg.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-cfg.so.${pkgver}"

    # create soname links
    for _lib in $(find "${pkgdir}" -name '*.so*'); do
        _soname="$(dirname ${_lib})/$(LC_ALL=C readelf -d "$_lib" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
        if [ ! -e "${_soname}" ]; then
            ln -s "$(basename ${_lib})" "${_soname}"
            ln -s "$(basename ${_soname})" "${_soname/.[0-9]*/}"
        fi
    done

    rm -rf "${pkgdir}"/usr/{include,share,bin}
    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -s $_pkgbasename "${pkgdir}/usr/share/licenses/${pkgname}"
}
