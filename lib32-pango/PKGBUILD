# Lib32 Packages for Chakra, part of chakra-project.org

_pkgbasename=pango
pkgname=lib32-$_pkgbasename
pkgver=1.40.14
pkgrel=1
pkgdesc="A library for layout and rendering of text (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-libthai' 'lib32-libgl' 'lib32-cairo' 'lib32-libxft'
         "$_pkgbasename=$pkgver")
makedepends=('gcc-multilib' 'lib32-mesa-libgl')
options=('!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver:0:4}/${_pkgbasename}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('90af1beaa7bf9e4c52db29ec251ec4fd0a8f2cc185d521ad1f88d01b3a6a17e3')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  sed -i 's#"pango.modules"#"pango.modules-32"#' pango/modules.c
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-included-modules=basic-fc \
              --with-dynamic-modules=arabic-fc,arabic-lang,basic-fc,basic-win32,basic-x,basic-atsui,hangul-fc,hebrew-fc,indic-fc,indic-lang,khmer-fc,syriac-fc,tibetan-fc \
              --disable-introspection
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin/pango-view,share,include}
}

