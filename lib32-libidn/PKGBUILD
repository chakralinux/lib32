# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=libidn
pkgname=lib32-$_pkgbasename
pkgver=1.16
pkgrel=1
_epoch=1
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications (32-bit)"
arch=('x86_64')
url="http://www.gnu.org/software/libidn/"
license=('custom')
depends=('lib32-gcc-libs' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
sha1sums=('33ff88e7111f07f3bb8aa888fdc7848bb319d4ce')


build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/$_pkgbasename-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}


package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
