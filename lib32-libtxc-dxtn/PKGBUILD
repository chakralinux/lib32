#
# part of chakra-project.org
#
# Maintainer: Francesco Marinucci <franzmari[at]chakra-project[dot]it>


pkgname=lib32-libtxc-dxtn
_pkgbasename=libtxc_dxtn
pkgver=1.0.1
pkgrel=2
arch=('x86_64')
pkgdesc="Texture compression library for Mesa (32-bit)"
url="http://dri.freedesktop.org/wiki/S3TC"
license=(custom:BSD)
depends=(lib32-mesa)
makedepends=(gcc-multilib)
options=(!libtool)
source=(http://cgit.freedesktop.org/~mareko/$_pkgbasename/snapshot/$_pkgbasename-$pkgver.tar.bz2)
md5sums=('63dc19246e1b8a5990be5ced66aff913')
 
build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  CC="gcc -m32" ./autogen.sh --prefix=/usr --libdir=/usr/lib32
  make
}
 
package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
 
  rm -rf "$pkgdir/usr/include"
 
  # License
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '5,22{s|^ \* \?||;p}' txc_dxtn.h \
    > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
