# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libsndfile
pkgname=lib32-$_pkgbasename
pkgver=1.0.25
pkgrel=2
pkgdesc="A C library for reading and writing files containing sampled sound (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('lib32-flac' 'lib32-libvorbis' $_pkgbasename=$pkgver)
makedepends=('lib32-alsa-lib' 'gcc-multilib')
options=('!libtool')
source=(http://www.mega-nerd.com/libsndfile/files/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('e2b7bb637e01022c7d20f95f9c3990a2')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export GETCONF="getconf -v POSIX_V7_ILP32_OFFBIG"
  export GETCONF_DIR="/usr/lib32/getconf"

  ./configure --prefix=/usr --disable-sqlite --libdir=/usr/lib32
  make -C src
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -C src DESTDIR="${pkgdir}" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA

  rm -rf "$pkgdir/usr/include"
}
