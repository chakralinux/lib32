# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Daniele Cocca <jmc@chakra-project.org>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=qt
pkgname="lib32-${_pkgbasename}"
pkgver=4.8.2
pkgrel=1
pkgdesc='A cross-platform application and UI framework. (ELF32)'
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL')
depends=(lib32-{fontconfig,sqlite3,alsa-lib,glib2,dbus-core,openssl}
         lib32-lib{png,tiff,mng,gl,sm,xrandr,xv,xi,xinerama,xcursor,xfixes,ffi}
         "${_pkgbasename}=${pkgver}")
makedepends=(cups gcc-multilib lib32-{mesa,libcups,libxfixes})
options=('!libtool')
_pkgfqn="${_pkgbasename}-everywhere-opensource-src-${pkgver}"
source=("http://releases.qt-project.org/qt4/source/${_pkgfqn}.tar.gz"
        'qt-webkit-4.8.1+glib-2.31.patch')
md5sums=('3c1146ddf56247e16782f96910a8423b'
         '1258b22d53a3d0a217d316e91cadc236')

build() {
  cd "${srcdir}/${_pkgfqn}"

  export QT4DIR="${srcdir}/${_pkgfqn}"
  export LD_LIBRARY_PATH="${QT4DIR}/lib:${LD_LIBRARY_PATH}"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # some of those are likely unnecessary, but I'm too lazy to find and remove them
  sed -i "/^QMAKE_LINK\s/s|g++|g++ -m32|g" mkspecs/common/g++-base.conf
  sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/g++-base.conf
  sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/gcc-base.conf
  sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS} -m32|g" mkspecs/common/gcc-base.conf
  sed -i "s|-Wl,-O1|-m32 -Wl,-O1|" mkspecs/common/g++-unix.conf
  sed -e "s|-O2|$CXXFLAGS -m32|" \
      -e "/^QMAKE_RPATH/s| -Wl,-rpath,||g" \
      -e "/^QMAKE_LINK\s/s|g++|g++ -m32|g" \
      -e "/^QMAKE_LFLAGS\s/s|+=|+= $LDFLAGS|g" \
      -i mkspecs/common/g++.conf

  # apply the needed patches
  patch -Np1 -i "${srcdir}/qt-webkit-4.8.1+glib-2.31.patch"

  ./configure -confirm-license -opensource -v -platform linux-g++-32 \
              -prefix /usr \
              -libdir /usr/lib32 \
              -plugindir /usr/lib32/qt/plugins \
              -importdir /usr/lib32/qt/imports \
              -datadir /usr/share/qt \
              -translationdir /usr/share/qt/translations \
              -sysconfdir /etc \
              -system-sqlite \
              -no-phonon \
              -no-phonon-backend \
              -webkit \
              -openssl-linked \
              -nomake demos \
              -nomake examples \
              -nomake docs \
              -nomake tools \
              -optimized-qmake \
              -no-rpath \
              -dbus-linked \
              -reduce-relocations \
              -no-openvg \
              -xcursor \
  || return 1

  # disable "warnings are errors" for some source directories
  sed -e 's,-Werror,,' \
      -i src/3rdparty/webkit/Source/WebCore/Makefile \
      -i src/3rdparty/webkit/Source/WebCore/Makefile.WebKit \
      -i src/3rdparty/webkit/Source/JavaScriptCore/Makefile \
      -i src/3rdparty/webkit/Source/JavaScriptCore/Makefile.WebKit \
      -i src/3rdparty/webkit/Source/WebKit/qt/Makefile.WebKit.QtWebKit \
  || return

  make
}

package() {
  cd "${srcdir}/${_pkgfqn}"
  make install INSTALL_ROOT="${pkgdir}"

  # Fix wrong path in pkgconfig files
  find "${pkgdir}/usr/lib32/pkgconfig" -type f -name '*.pc' \
       -exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;
  # Fix wrong path in prl files
  find "${pkgdir}/usr/lib32" -type f -name '*.prl' \
       -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

  rm -rf "${pkgdir}/usr"/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgbasename}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
