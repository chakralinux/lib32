#maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
#contributions from Arch: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/lib32-jack2

pkgname=lib32-jack2
pkgver=1.9.9.5
pkgrel=1
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL')
depends=('lib32-gcc-libs' 'jack2')
makedepends=('python2' 'gcc-multilib')
provides=('jack2-multilib' 'jack2-dbus-multilib' )
conflicts=('jack2-multilib' 'jack2-dbus-multilib')
replaces=('jack2-multilib' 'jack2-dbus-multilib')
source=("http://jackaudio.org/downloads/jack-${pkgver}.tar.bz2")
md5sums=('6c9de6b89db9d7076fa2ce222816cf4c')

prepare() {
  cd "$srcdir/jack-$pkgver"

  # ugly dynamic patch to build just the 32 bit jack client lib
  # (compatible with previous releases and hoping with the next)
  sed -e '/serverlib/,/clientlib/{/clientlib/!d}' \
      -e '/clientlib32bit/i\\tclientlib.posted = True' \
      -e '1,/clientlib32bit/!d' \
      -i common/wscript

  # celt and opus are not required to build
  sed -i 's/, "CELT", "OPUS"//' common/wscript
}

build() {
  cd "$srcdir/jack-$pkgver"
  python2 waf configure --prefix=/usr \
                        --libdir=/usr/lib32 \
                        --mixed
  cd common
  python2 ../waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/jack-$pkgver/build/common"

  # jack client 32 bit lib
  install -Dm755 libjack.so "$pkgdir/usr/lib32/libjack.so.0.1.0"
  ln -s libjack.so.0.1.0 "$pkgdir/usr/lib32/libjack.so.0"
  ln -s libjack.so.0.1.0 "$pkgdir/usr/lib32/libjack.so"
}
