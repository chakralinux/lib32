pkgname=lib32-curl
_pkgname=curl
pkgver=7.54.0
pkgrel=1
pkgdesc="An URL retrieval utility and library (32-bit)"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('lib32-libssh2' 'lib32-krb5' 'lib32-libidn' "${_pkgname}=${pkgver}")
makedepends=('lib32-gcc-libs')
source=("http://curl.haxx.se/download/$_pkgname-$pkgver.tar.bz2")
md5sums=('89bb7ba87384dfbf4f1a3f953da42458')

build() {
  cd "$_pkgname-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --libdir=/usr/lib32 \
      --disable-dependency-tracking \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --enable-manual \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --with-gssapi \
      --with-libidn \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{share,bin}
  
  # license
  install -d "$pkgdir/usr/share/licenses"
  ln -s "$_pkgname" "$pkgdir/usr/share/licenses/$pkgname"

  # devel
  find "${pkgdir}/usr/include/curl" -type f -not -name curlbuild.h -delete
  mv "$pkgdir/usr/include/curl/curlbuild.h" "$pkgdir/usr/include/$_pkgname/curlbuild-32.h"
}
