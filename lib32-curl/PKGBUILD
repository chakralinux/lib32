# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=curl
pkgname=lib32-$_pkgbasename
pkgver=7.21.3
pkgrel=1
pkgdesc="An URL retrieval utility and library (32-bit)"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('lib32-zlib' 'lib32-openssl' 'bash' 'ca-certificates' $_pkgbasename=$pkgver)
makedepends=(gcc-multilib)
options=('!libtool')
source=("http://curl.haxx.se/download/${_pkgbasename}-${pkgver}.tar.bz2")
md5sums=('5b57fee22090b5c43a6886fdd35af2ce')


build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure \
      --with-random=/dev/urandom \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-dependency-tracking \
      --enable-ipv6 \
      --disable-ldaps \
      --disable-ldap \
      --enable-manual \
      --enable-versioned-symbols \
      --with-ca-path=/etc/ssl/certs \
      --without-libidn \
      --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{share,bin}
  #we need curlbuild.h
  find ${pkgdir}/usr/include/curl -type f -not -name curlbuild.h -delete
  #now save it as curlbuild-32.h
  mv ${pkgdir}/usr/include/curl/curlbuild.h ${pkgdir}/usr/include/curl/curlbuild-32.h

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
