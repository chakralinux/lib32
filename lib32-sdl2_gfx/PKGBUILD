# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=sdl2_gfx
pkgname=lib32-$_pkgbasename
pkgver=1.0.0
pkgrel=1
pkgdesc="SDL Graphic Primitives (Version 2, 32-bit)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('zlib')
depends=('lib32-sdl2' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=(!libtool)
source=("http://www.ferzkopp.net/Software/SDL2_gfx/SDL2_gfx-${pkgver}.tar.gz")
md5sums=('f008cc117cb901aec6bf5c2f0b11f7ea')

build() {
  cd "${srcdir}/SDL2_gfx-${pkgver}"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  ./autogen.sh
  ./configure --disable-static \
              --prefix=/usr \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/SDL2_gfx-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

