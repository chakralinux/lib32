# Part of the X.org group
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# Contributor (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxtst
pkgname=lib32-$_pkgbasename
pkgver=1.2.2
pkgrel=1
pkgdesc="X11 Testing -- Resource extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxext' 'lib32-libxi' 'recordproto' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib' 'xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXtst-${pkgver}.tar.bz2)
md5sums=('25c6b366ac3dc7a12c5d79816ce96a59')

build() {
  cd "${srcdir}/libXtst-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXtst-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
