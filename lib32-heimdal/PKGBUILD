# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=heimdal
pkgname=lib32-$_pkgbasename
pkgver=1.3.3
pkgrel=3
pkgdesc="Implementation of Kerberos V5 libraries (32-bit)"
arch=('x86_64')
url="http://www.h5l.org/"
license=('custom')
depends=('lib32-e2fsprogs' 'lib32-openssl' 'lib32-db' lib32-sqlite3 $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://www.h5l.org/dist/src/${_pkgbasename}-${pkgver}.tar.gz
        001_all_heimdal-no_libedit.patch
        002_all_heimal-fPIC.patch
        003_all_heimdal-rxapps.patch
        005_all_heimdal-suid_fix.patch
        012_all_heimdal-berkdb.patch
        013_all_heimdal-pthread-lib.patch
        014_all_heimdal-path.patch)
md5sums=('963c09f1b14c41660be70b55fae9f163'
         '98e28f11f906c967aac22d6184102c9e'
         '6d5571bdedba2e2423b90bccdbac2c0a'
         '2feec3924ee5230b54175b4d4000c872'
         '45aeb207f360f9f4e9e0fabc8bfeecbc'
         '56f5d10d0ec40f2fda82ef144ffac1e0'
         '1b8665b771c4eb6b56ea8582c96e56e3'
         '8208ae8c0b6ff5ab4f64af1693e9e396')

build() {
  cd ${srcdir}/heimdal-${pkgver}
  patch -Np0 -i ${srcdir}/001_all_heimdal-no_libedit.patch
  patch -Np0 -i ${srcdir}/002_all_heimal-fPIC.patch
  patch -Np0 -i ${srcdir}/003_all_heimdal-rxapps.patch
  patch -Np0 -i ${srcdir}/005_all_heimdal-suid_fix.patch
  patch -Np1 -i ${srcdir}/012_all_heimdal-berkdb.patch
  patch -Np1 -i ${srcdir}/013_all_heimdal-pthread-lib.patch
  patch -Np0 -i ${srcdir}/014_all_heimdal-path.patch

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  libtoolize --force
  aclocal -I cf
  autoconf
  automake

  ./configure --prefix=/usr --enable-shared=yes --without-x \
        --libdir=/usr/lib32 \
        --with-openssl-lib=/usr/lib32 \
        --with-readline-lib=/usr/lib32 \
        --with-readline-include=/usr/include/readline \
        --with-sqlite3-lib=/usr/lib32 \
        --with-sqlite3-include=/usr/include
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{etc,bin,sbin,include,share,libexec}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
