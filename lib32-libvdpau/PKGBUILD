# Maintainer: AlmAck

_pkgbasename=libvdpau
pkgname=lib32-libvdpau
pkgver=0.7
pkgrel=1
pkgdesc='Nvidia VDPAU library'
arch=('x86_64')
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
license=('custom')
depends=('lib32-gcc-libs' 'lib32-libxext' $_pkgbasename=$pkgver)
makedepends=('dri2proto')
source=("http://people.freedesktop.org/~aplattner/vdpau/${_pkgbasename}-${pkgver}.tar.gz")
md5sums=('cb81b0c3b7d32b2b2a51894ef05b95ce')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --sysconfdir='/etc'
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}