# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=libwebp
pkgname=lib32-$_pkgbasename
pkgver=0.3.1
pkgrel=1
pkgdesc="WebP library and conversion tools (32-bit)."
arch=('x86_64' 'i686')
url="http://code.google.com/intl/en/speed/webp/"
license=('BSD')
depends=('lib32-libpng' 'lib32-libjpeg' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://webp.googlecode.com/files/$_pkgbasename-$pkgver.tar.gz")
md5sums=('dc862bb4006d819b7587767a9e83d31f')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
