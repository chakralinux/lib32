#
# Apps Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe cal√† <jiveaxe@gmail.com>

pkgname=q4wine
pkgver=0.121
pkgrel=2
pkgdesc="A Qt4 GUI for Wine"
arch=("x86_64")
url="http://sourceforge.net/projects/${pkgname}/"
license=("GPL3")
depends=("qt" "wine" "sqlite3" "which" "icoutils")
makedepends=("cmake")
optdepends=("winetricks")
changelog=${pkgname}.changelog
options=('!emptydirs')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/_/-}.tar.bz2"
        "${pkgname}.changelog"
        "q4wine-gcc47.patch")
sha256sums=('fc5a1a8c09d133a17a997a2b020ad03eaf06c81357863d9f69fd7206d86e3edc'
            '7360b8dca1a07686be66ab79bb469f43f2a0462c0f410829074385570f2415bf'
            'dae09824fd5a2c3d6ecb389bacdde3fc1eb43995b398dec6bd821b6127e4003a')

prepare() {
  cd "${pkgname}-${pkgver/_/-}"
  patch -Np1 -i "${srcdir}/q4wine-gcc47.patch"
  install -Dm644 ChangeLog "${startdir}/${pkgname}.changelog"
}

build() {
  cd "${pkgname}-${pkgver/_/-}"

  [[ -d build ]] && rm -rf build 
  mkdir build && cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBS_ENTRY_PATH=/usr/lib/${pkgname} \
    -DWITH_WINETRIKS=ON \
    ..
  make
}

package() {
  cd "${pkgname}-${pkgver/_/-}"/build
  make DESTDIR="${pkgdir}" install
}
