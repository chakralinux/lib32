pkgname=q4wine
pkgver=1.3.5
pkgrel=1
pkgdesc="Qt GUI for Wine"
arch=("x86_64")
url="http://sourceforge.net/projects/${pkgname}/"
license=("GPL3")
depends=("qt5-base" "wine" "sqlite3" "which" "icoutils" "fuseiso")
makedepends=("cmake" "qt5-tools")
optdepends=("winetricks")
options=('!emptydirs')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/_/-}.tar.bz2")
md5sums=('31c0bf8a3e7da419f727b716c3be58e8')

build() {
  cd ${srcdir}/${pkgname}-${pkgver/_/-}

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DQT5=ON \
        -DLIBS_ENTRY_PATH=/usr/lib/$pkgname .
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver/_/-}
  make DESTDIR=${pkgdir} install
}
