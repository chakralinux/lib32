# maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

pkgname=lib32-libpulse
_pkgbasename=pulseaudio
pkgdesc="A featureful, general-purpose sound server. (32-bit client libraries)"
pkgver=4.0
pkgrel=3
arch=('x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=(lib32-{dbus-core,xcb-util,json-c}
         lib32-lib{asyncns,cap,xtst,sm,sndfile,samplerate}
         'webrtc-audio-processing' 'systemd'
         "$_pkgbasename=${pkgver}")
makedepends=('pkgconfig' 'gcc-multilib' 'lib32-libtool' 'intltool'
		         'lib32-glib2' 'lib32-speex')
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio")
conflicts=('lib32-pulseaudio')
replaces=('lib32-pulseaudio')
options=(!emptydirs !libtool)
source=("http://freedesktop.org/software/pulseaudio/releases/pulseaudio-${pkgver}.tar.xz")
sha256sums=('35ceb36bb1822fe54f0b5e4863b4f486769fdfb8ff2111f01fd8778928f9cdae')

_libs="libpulse.la libpulse-simple.la libpulse-mainloop-glib.la"
#_pkglibs="libpulsecommon-${pkgver}.la libpulsedsp.la"
_pkglibs="libpulsecommon-4.0.la libpulsedsp.la"


build() {
  cd "$srcdir/pulseaudio-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-hal \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -C src ${_libs} ${_pkglibs}
}

package() {
  cd "${srcdir}/pulseaudio-${pkgver}"
  make -C src DESTDIR="${pkgdir}" \
    lib_LTLIBRARIES="${_libs}" pkglib_LTLIBRARIES="${_pkglibs}" \
    install-libLTLIBRARIES install-pkglibLTLIBRARIES
  make DESTDIR="${pkgdir}" install-pkgconfigDATA
}
