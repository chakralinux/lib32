# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor from AUR: 3V0LU710N <db_eee-at-hotmail-dot-com>

_pkgbasename=libusb
pkgname=lib32-$_pkgbasename
pkgver=1.0.19
pkgrel=1
pkgdesc="Library to enable user space application programs to communicate with USB devices. (32-bit). libusb fork."
arch=('x86_64')
url="http://libusbx.sourceforge.net/"
license=('LGPL')
depends=('lib32-glibc' 'lib32-systemd' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
provides=('lib32-libusbx')
conflicts=('lib32-libusbx')
replaces=('lib32-libusbx<=1.0.18')
source=(http://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)
options=(!libtool)
md5sums=('f9e2bb5879968467e5ca756cb4e1fa7e')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package () {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}
