# Arch contributor: https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/lib32-libelf

_pkgbasename=elfutils
pkgname=lib32-libelf
pkgver=0.175
pkgrel=2
pkgdesc="Libraries to handle ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('lib32-bzip2' 'lib32-zlib' "libelf=$pkgver")
makedepends=('gcc-multilib')
provides=('lib32-elfutils')
conflicts=('lib32-elfutils')
replaces=('lib32-elfutils')
source=(https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
options=('staticlibs')
sha256sums=('f7ef925541ee32c6d15ae5cb27da5f119e01a5ccdbe9fe57bf836730d7b7a65b'
            'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A') # Mark J. Wielaard <mark@klomp.org>

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
	
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
