# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=sdl2_net
pkgname=lib32-$_pkgbasename
pkgver=249
pkgrel=1
pkgdesc="A small sample cross-platform networking library (Version 2, 32-bit)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=(lib32-sdl2 $_pkgbasename=$pkgver)
makedepends=(mercurial 'gcc-multilib')
options=(!libtool)
source=("sdl2_net::hg+http://hg.libsdl.org/SDL_net#revision=$pkgver")
md5sums=('SKIP')

build() {
  cd "${srcdir}/sdl2_net/"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  # sdl2_net requires automake 1.13 but it compiles fine with current version (1.11) in [core]
  sed -i 's/1.13/1.11/g' ./configure
  
  ./autogen.sh
  ./configure --disable-static --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/sdl2_net/"
  make DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

