# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=readline
pkgname=lib32-$_pkgbasename
_patchlevel=001 #prepare for some patches
pkgver=6.2.$_patchlevel
pkgrel=3
pkgdesc="GNU readline library (32-bit)"
arch=(x86_64)
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('lib32-glibc' 'lib32-ncurses' $_pkgbasename)
makedepends=('gcc-multilib')
source=(http://ftp.gnu.org/gnu/readline/readline-6.2.tar.gz)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$_patchlevel; p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-6.2-patches/readline62-$(printf "%03d" $p))
    done
fi
md5sums=('67948acb2ca081f23359d0256e9a271c'
         '83287d52a482f790dfb30ec0a8746669')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-6.2
  for p in ../readline62-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p}
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  ./configure --prefix=/usr --libdir=/usr/lib32
  make SHLIB_LIBS=-lncurses
}

package() {
  cd ${srcdir}/${_pkgbasename}-6.2

  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
