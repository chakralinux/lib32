# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=acl
pkgname=lib32-acl
pkgver=2.2.51
pkgrel=1
pkgdesc="Access control list libraries (32-bit)"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
depends=('lib32-attr' 'acl')
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://download.savannah.gnu.org/releases/${_pkgbasename}/${_pkgbasename}-${pkgver}.src.tar.gz)
sha256sums=('06854521cf5d396801af7e54b9636680edf8064355e51c07657ec7442a185225')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export INSTALL_USER=root INSTALL_GROUP=root 
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32
  make 
}

package() {
  cd "${srcdir}/acl-${pkgver}"

  make DIST_ROOT="${pkgdir}" install install-lib install-dev

  rm -rf ${pkgdir}/usr/{bin,include,share}
}
