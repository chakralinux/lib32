# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer: Giuseppe Cala' <gcala[at]chakra-project[dot]org>

pkgname=wine
pkgver=1.7.12
pkgrel=1
_pkgbasever=${pkgver/rc/-rc}
source=(http://ibiblio.org/pub/linux/system/emulators/$pkgname/$pkgname-$_pkgbasever.tar.bz2
        30-win32-aliases.conf
        http://dev.gentoo.org/~tetromino/distfiles/wine/winepulse-patches-$pkgver.tar.bz2)

md5sums=('ce32c886af1c8a4e824e8dc1a01c5703'
         '1ff4e467f59409272088d92173a0f801'
         '79649b4c374a09054f934010b47c7132')

pkgdesc="A compatibility layer for running Windows programs"
url="http://www.winehq.com"
categories=('system')
screenshot=('http://wstaw.org/m/2012/04/28/wine.png')
arch=(x86_64)
license=(LGPL)
install=wine.install

depends=(
  fontconfig            lib32-fontconfig
  mesa                  lib32-mesa
  libglapi              lib32-libglapi
  libxcursor            lib32-libxcursor
  libxrandr             lib32-libxrandr
  libxdamage            lib32-libxdamage
  libxxf86dga           lib32-libxxf86dga
  alsa-lib              lib32-alsa-lib
  lcms                  lib32-lcms
  mpg123                lib32-mpg123
  openal                lib32-openal
  libxml2               lib32-libxml2
  libxi                 lib32-libxi
  gettext               lib32-gettext
  gstreamer0.10-base    lib32-gstreamer0.10-base
  libpulse              lib32-libpulse
  desktop-file-utils
)



makedepends=(autoconf ncurses bison perl fontforge flex prelink
  'gcc>=4.5.0-2' 'gcc-multilib>=4.5.0-2'
  giflib         lib32-giflib
  libxpm         lib32-libxpm
  libpng         lib32-libpng
  libxinerama    lib32-libxinerama
  libxcomposite  lib32-libxcomposite
  libxmu         lib32-libxmu
  libxxf86vm     lib32-libxxf86vm
  libxml2        lib32-libxml2
  libxslt        lib32-libxslt
  libldap        lib32-libldap
  lcms           lib32-lcms
  mpg123         lib32-mpg123
  openal         lib32-openal
  jack           lib32-jack
  libcups        lib32-libcups
  gnutls         lib32-gnutls
  v4l-utils      lib32-v4l-utils
  alsa-lib       lib32-alsa-lib
  samba
  opencl-headers
)

optdepends=(
  giflib        lib32-giflib
  libpng        lib32-libpng
  libldap       lib32-libldap
  jack          lib32-jack
  libcups       lib32-libcups
  gnutls        lib32-gnutls
  v4l-utils     lib32-v4l-utils
  cups
  samba
  dosbox
)



build() {
  cd "$srcdir"
  
  rm -rf $pkgname

  # Allow ccache to work
  mv $pkgname-$_pkgbasever $pkgname

  # ncurses fix
  sed -i 's|libncurses|libncursesw|g' "$srcdir/$pkgname/configure"
  sed -i 's|lncurses|lncursesw|g' "$srcdir/$pkgname/configure"

  cd $pkgname
  for _patch in $(ls $srcdir/winepulse-patches-$pkgver/*.patch) ; do    
    patch -Np1 -i $_patch
  done
  cd $srcdir

  # Get rid of old build dirs
  rm -rf $pkgname-{32,64}-build

  # These additional CPPFLAGS solve FS#27662 and FS#34195
  export CPPFLAGS="${CPPFLAGS/-D_FORTIFY_SOURCE=2/} -D_FORTIFY_SOURCE=0"

  msg2 "Building Wine-64..."
  mkdir $pkgname-64-build
  cd "$srcdir/$pkgname-64-build"
  ../$pkgname/configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --with-x \
    --without-gstreamer \
    --with-pulse \
    --enable-win64
    # Gstreamer was disabled for FS#33655

  make

  _wine32opts=(
    --libdir=/usr/lib32
    --with-wine64="$srcdir/$pkgname-64-build"
  )

  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  msg2 "Building Wine-32..."
  mkdir "$srcdir/$pkgname-32-build"
  cd "$srcdir/$pkgname-32-build"
  ../$pkgname/configure \
    --prefix=/usr \
    --with-x \
    --with-pulse \
    --without-gstreamer \
    "${_wine32opts[@]}"


  # These additional flags solve FS#23277
  make CFLAGS+="-mstackrealign -mincoming-stack-boundary=2" CXXFLAGS+="-mstackrealign -mincoming-stack-boundary=2"
}

package() {
  msg2 "Packaging Wine-32..."
  cd "$srcdir/$pkgname-32-build"
  make prefix="$pkgdir/usr" \
    libdir="$pkgdir/usr/lib32" \
    dlldir="$pkgdir/usr/lib32/wine" install

  msg2 "Packaging Wine-64..."
  cd "$srcdir/$pkgname-64-build"
  make prefix="$pkgdir/usr" \
    libdir="$pkgdir/usr/lib" \
    dlldir="$pkgdir/usr/lib/wine" install

  # Font aliasing settings for Win32 applications
  install -d "$pkgdir"/etc/fonts/conf.{avail,d}
  install -m644 "$srcdir/30-win32-aliases.conf" "$pkgdir/etc/fonts/conf.avail"
  ln -s ../conf.avail/30-win32-aliases.conf "$pkgdir/etc/fonts/conf.d/30-win32-aliases.conf"
}

