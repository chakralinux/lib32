# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Calà <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxcomposite
pkgname=lib32-$_pkgbasename
pkgver=0.4.3
pkgrel=2
pkgdesc="X11 Composite extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes' 'lib32-libxext' 'compositeproto' $_pkgbasename)
makedepends=('xorg-util-macros' gcc-multilib)
options=(!libtool)
source=(${url}/releases/individual/lib/libXcomposite-${pkgver}.tar.bz2)
sha1sums=('081b26b556d55e20d7956c80a2ea2854962aecec')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXcomposite-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXcomposite-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
