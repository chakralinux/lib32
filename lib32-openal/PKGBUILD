# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

# NOTE: it must be built without ffmpeg installed, at least until a lib32 package for ffmpeg is provided.

_pkgbasename=openal
pkgname=lib32-$_pkgbasename
pkgver=1.14
pkgrel=2
pkgdesc="OpenAL audio library for use with opengl (32-bit)"
arch=('x86_64')
url="http://www.openal.org/"
license=('LGPL')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('lib32-alsa-lib' 'lib32-sdl' 'lib32-libpulse' 'pkgconfig' 'cmake' gcc-multilib)
options=('!libtool')
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-${pkgver}.tar.bz2)
md5sums=('3d8b86c21a2f87a2a5e60f78f3b3f03d')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-soft-${pkgver}/build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_SUFFIX=32 \
    ..
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-soft-${pkgver}/build
  make DESTDIR=${pkgdir}/ install 

  # Remove files already provided by the 64-bit package.
  rm -rf "${pkgdir}"/usr/{include,share,bin}

  # Licenses.
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
