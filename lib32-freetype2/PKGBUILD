# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename
pkgver=2.5.5
_ubuntu_ver=2.5.2
pkgrel=1
_ubuntu_diff_version=${_ubuntu_ver}-1ubuntu2
pkgdesc="TrueType font rendering library (32-bit)"
arch=(x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' 'lib32-harfbuzz' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
provides=("${pkgname}-ubuntu")
conflicts=("${pkgname}-ubuntu")
replaces=("${pkgname}-ubuntu")
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        "http://archive.ubuntu.com/ubuntu/pool/main/f/freetype/freetype_${_ubuntu_diff_version}.diff.gz")
md5sums=('2a7a314927011d5030903179cf183be0'
         'eda9c925032709bbd7463edcded05881')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/freetype-${pkgver}"
  
  # apply ubuntu patches
  patch -Np1 -i ../freetype_${_ubuntu_diff_version}.diff
  patch -p0 -i "${srcdir}/freetype-${pkgver}/debian/patches-freetype/freetype-2.1.7-backwards.compat.patch"
  patch -p1 -i "${srcdir}/freetype-${pkgver}/debian/patches-freetype/enable-subpixel-rendering.patch"
  patch -p1 -i "${srcdir}/freetype-${pkgver}/debian/patches-freetype/enable-gxvalid-otvalid.patch"
  patch -p1 -i "${srcdir}/freetype-${pkgver}/debian/patches-freetype/revert_scalable_fonts_metric.patch"

  ./configure --prefix=/usr \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir"/{etc,var}
}
