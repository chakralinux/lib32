# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libice
pkgname=lib32-$_pkgbasename
pkgver=1.0.8
pkgrel=1
pkgdesc="X11 Inter-Client Exchange library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
depends=('lib32-glibc' 'xproto' $_pkgbasename=$pkgver)
makedepends=('pkgconfig' 'xtrans' 'gcc-multilib')
options=('!libtool')
source=(${url}/releases/individual/lib/libICE-${pkgver}.tar.bz2)
license=('custom')
sha1sums=('ddb14df8bbc43df7322978f5f9f802936e2a7324')

build() {
  cd "${srcdir}/libICE-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32

  make
}

package() {
  cd "${srcdir}/libICE-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
