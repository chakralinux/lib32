# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=attr
pkgname=lib32-$_pkgbasename
pkgver=2.4.46
pkgrel=1
pkgdesc="Extended attribute support library for ACL support (32-bit)"
arch=(x86_64)
url="http://oss.sgi.com/projects/xfs/"
license=('LGPL')
depends=('lib32-glibc' $_pkgbasename) 
makedepends=('gcc-multilib' 'gettext')
options=('!libtool')
source=(http://download.savannah.gnu.org/releases/${_pkgbasename}/${_pkgbasename}-${pkgver}.src.tar.gz)
sha256sums=('dcd69bdca7ff166bc45141eddbcf21967999a6b66b0544be12a1cc2fd6340e1f')

build() {
  cd $srcdir/attr-$pkgver 

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  autoconf
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32
  make 

  # Doesn't like building as non-root

  make prefix=${pkgdir}/usr \
    PKG_LIB_DIR=$pkgdir/usr/lib32 \
    PKG_DEVLIB_DIR=$pkgdir/usr/lib32 \
    install-lib install-dev

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
