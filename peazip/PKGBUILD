# Maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>
# Contributions from: Daniele Cocca

pkgname=peazip
pkgver=5.3.1
pkgrel=1
pkgdesc="Free, Open Source file and archive manager. Supports 7Z, ACE, ARC, CAB, ISO, PAQ, PEA, RAR, TAR, ZIP and many more archive formats."
arch=('x86_64')
url="http://peazip.org/"
license=('GPL')
depends=('zip' 'unzip' 'unrar' 'xz' 'p7zip' 'unarj' 'lib32-qt' 'lib32-fontconfig')
optdepends=('rar: support for RAR archive compression')
conflicts=('lib32-fpc-qtbindings') # just in case it will be added - the PKGBUILD would need a change
source=("http://sourceforge.net/projects/peazip/files/${pkgver}/${pkgname}-${pkgver}.LINUX.Qt.tgz")
noextract=("${pkgname}-${pkgver}.LINUX.Qt.tgz")
md5sums=('74fae026d090e989c0184e769e646546')

package() {
  # Extract the software as distributed upstream
  tar -zxf "${srcdir}/${pkgname}-${pkgver}.LINUX.Qt.tgz" -C "${pkgdir}"

  # Remove useless KDE3 support directories or empty ones
  rm -rf "${pkgdir}/opt"
  rm -rf "${pkgdir}/usr/lib32"
  rm -rf "${pkgdir}/usr/local/lib"
  rm -rf "${pkgdir}/usr/local/lib32"
  rm -rf "${pkgdir}/usr/local/share/PeaZip/res/icons"
  rm -rf "${pkgdir}/usr/local/share/PeaZip/res/unace"
  rm -rf "${pkgdir}/usr/local/share/PeaZip/FreeDesktop_integration/kde3-konqueror"

  # Move the libQtPas library to a place coherent with the rest of the lib32 packages
  mv "${pkgdir}/usr/lib"{,32}

  # Move files from /usr/local/share to /usr/share prefix
  for directory in "${pkgdir}/usr/local/share"/*; do
    mv "${directory}" "${directory}/../../../share"
  done
  rm -rf "${pkgdir}/usr/local/share"

  # Move files from /usr/local to /usr prefix
  for directory in "${pkgdir}/usr/local"/*; do
    mv "${directory}" "${directory}/../../"
  done
  rm -rf "${pkgdir}/usr/local"

  # Recreate the application launchers
  ln -sf /usr/share/PeaZip/res/pea "${pkgdir}/usr/bin/pea"
  ln -sf /usr/share/PeaZip/res/pealauncher "${pkgdir}/usr/bin/pealauncher"
  ln -sf /usr/share/PeaZip/peazip "${pkgdir}/usr/bin/peazip"
}
