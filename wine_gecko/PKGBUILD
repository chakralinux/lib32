# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

pkgname=wine_gecko
pkgver=1.8
pkgrel=1
pkgdesc="Wine's built-in replacement for Microsoft's Internet Explorer"
arch=(i686 x86_64)
url="http://wiki.winehq.org/Gecko"
license=(MPL)
depends=('wine>=1.5')
categories=('system')
source=(http://downloads.sourceforge.net/project/wine/Wine%20Gecko/${pkgver}/${pkgname}-${pkgver}-x86{,_64}.msi)

md5sums=('c2f19f07e399d8b9d75a14f37fdddca7'
         '5ce40ef8e3fd56d2c8c4e8d938f10f7b')

if [[ $CARCH == i686 ]]; then
  # Strip x86_64 msi
  source=(${source[0]})
  md5sums=(${md5sums[0]})
fi

package() {
  cd "$srcdir"
  _geckodir="$pkgdir/usr/share/wine/gecko"
  install -Dm644 $pkgname-$pkgver-x86.msi "$_geckodir/$pkgname-$pkgver-x86.msi"
  if [[ $CARCH == x86_64 ]]; then
    install -m644 $pkgname-$pkgver-x86_64.msi "$_geckodir/"
  fi
}
