# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

pkgname=wine_gecko
pkgver=1.2.0
pkgrel=2
pkgdesc="Wine's built-in replacement for Microsoft's Internet Explorer"
arch=(i686 x86_64)
url="http://wiki.winehq.org/Gecko"
license=(MPL)
depends=('wine>=1.3.16')
source=(http://downloads.sourceforge.net/project/wine/Wine%20Gecko/${pkgver}/${pkgname}-${pkgver}-x86{,_64}.msi)
md5sums=('f2ed548494c86c511892b1c02491c8c4'
         '26489d8d1dfa34b5783b3e567edcaa9f')

if [[ $CARCH == i686 ]]; then
  # Strip x86_64 msi
  source=(${source[0]})
  md5sums=(${md5sums[0]})
fi

package() {
  cd "$srcdir"

  # Install 32-bit
  install -Dm644 $pkgname-$pkgver-x86.msi "$pkgdir/usr/share/wine/gecko/$pkgname-$pkgver-x86.msi"

  if [[ $CARCH == x86_64 ]]; then 
    # Install 64-bit
    install -m644 $pkgname-$pkgver-x86_64.msi "$pkgdir/usr/share/wine/gecko/"
  fi
}
