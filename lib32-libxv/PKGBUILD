_pkgbasename=libxv
pkgname=lib32-$_pkgbasename
pkgver=1.0.10
pkgrel=2
pkgdesc="X11 Video extension library (32-bit)"
arch=(x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext' 'videoproto' $_pkgbasename=$pkgver)
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool')
source=(${url}/releases/individual/lib/libXv-${pkgver}.tar.bz2)
sha1sums=('1e93df036bb2bb01e85b7c8886760affb33b8e88')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/libXv-${pkgver}
  ./configure --prefix=/usr --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/libXv-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
