# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer: Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=glib2
pkgname="lib32-${_pkgbasename}"
pkgver=2.34.3
_pkgver=2.34
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs. (ELF32)"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=('lib32-pcre' 'lib32-zlib' 'lib32-dbus-core' "${_pkgbasename}")
makedepends=('python2' 'lib32-libffi' 'gcc-multilib')
options=('!libtool' '!docs')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${_pkgver}/glib-${pkgver}.tar.xz")
sha256sums=('855fcbf87cb93065b488358e351774d8a39177281023bae58c286f41612658a7')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/glib-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32 \
      --enable-static --enable-shared --with-pcre=system --disable-fam
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{share,include}}

  cd "${pkgdir}"/usr/bin
  mv gio-querymodules gio-querymodules-32
  rm -f gdbus glib* gobject-query gsettings gtester*
  rm -rf "$pkgdir"/usr/lib32/gdbus-2.0
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules-32 -delete
}
