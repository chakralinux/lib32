_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.54.2
pkgrel=2
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (32-bit)"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=('lib32-libdbus' 'lib32-libffi' 'lib32-pcre' 'lib32-libelf' 'lib32-util-linux' "${_pkgbasename}=${pkgver}")
makedepends=('gcc-multilib' 'python3')
options=('!docs')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-${pkgver}.tar.xz")
sha256sums=('bb89e5c5aad33169a8c7f28b45671c7899c12f74caf707737f784d7102758e6c')

prepare() {
  cd "${srcdir}/glib-${pkgver}"

}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/glib-${pkgver}"
  PYTHON=/usr/bin/python3 ./configure --prefix=/usr --sysconfdir=/etc \
      --libdir=/usr/lib32 --with-pcre=system --disable-fam --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{share,include}}

  cd "${pkgdir}"/usr/bin
  mv gio-querymodules gio-querymodules-32
  rm -f gdbus glib* gobject-query gsettings gtester*
  rm -rf "$pkgdir"/usr/lib32/gdbus-2.0
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules-32 -delete
}
