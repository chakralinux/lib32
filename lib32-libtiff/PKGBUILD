# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libtiff
pkgname=lib32-$_pkgbasename
pkgver=3.9.4
pkgrel=1
pkgdesc="Library for manipulation of TIFF images (32-bit)"
arch=('x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('lib32-libjpeg' 'lib32-zlib' $_pkgbasename)
makedepends=('lib32-libgl' 'lib32-libxmu' 'lib32-libxi' gcc-multilib)
options=('!libtool')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz \
        libtiff-CVE-2009-2285.patch)
md5sums=('2006c1bdd12644dbf02956955175afd6' 'ff61077408727a82281f77a94f555e2a')
sha1sums=('a4e32d55afbbcabd0391a9c89995e8e8a19961de' 'eadce8c8bd72ea9c74f35300bf299131813b0c8b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/tiff-${pkgver}"
  patch -p1 < ../libtiff-CVE-2009-2285.patch
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
