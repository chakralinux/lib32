# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libtiff
pkgname=lib32-$_pkgbasename
pkgver=4.0.3
pkgrel=1
pkgdesc="Library for manipulation of TIFF images (32-bit)"
arch=('x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('lib32-libjpeg-turbo' 'lib32-zlib' $_pkgbasename=$pkgver)
makedepends=('lib32-libgl' 'lib32-libxmu' 'lib32-libxi' gcc-multilib)
options=('!libtool')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz)
md5sums=('051c1068e6a0627f461948c365290410')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/tiff-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
