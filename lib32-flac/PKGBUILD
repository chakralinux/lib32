# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=flac
pkgname=lib32-$_pkgbasename
pkgver=1.2.1
pkgrel=2
pkgdesc="Free Lossless Audio Codec (32-bit)"
arch=('x86_64')
url="http://flac.sourceforge.net/"
license=('custom:Xiph' 'LGPL' 'GPL' 'FDL')
depends=('lib32-libogg' 'lib32-gcc-libs' $_pkgbasename=$pkgver)
makedepends=('nasm' gcc-multilib)
options=('!libtool' '!makeflags')
source=(http://downloads.sf.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz
        flac-1.2.1-gcc-4.3-includes.patch)
md5sums=('153c8b15a54da428d1f0fadc756c22c7'
         'b9d245422bbc547b18a72897366bea77')
sha1sums=('bd54354900181b59db3089347cc84ad81e410b38'
          '7e9f8c2ef4d9daf5ddd20df63b608fe19b8c1560')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}
  patch -Np1 -i ${srcdir}/flac-1.2.1-gcc-4.3-includes.patch
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared \
              --disable-rpath --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
