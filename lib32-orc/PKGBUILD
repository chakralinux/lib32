#
# Lib32 Packages for Chakra, part of chakra-project.org
#
# Maintainer: kote <koteccr at gmail dot com>
# Arch linux maintainer:
# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgbasename=orc
pkgname=lib32-${_pkgbasename}
pkgver=0.4.18
pkgrel=2
pkgdesc="The Oild Runtime Compiler. Multilib"
arch=('x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://code.entropywave.com/download/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz")
md5sums=('1a2552e8d127526c48d644fe6437b377')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  # multilib
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Removing unneeded files
  rm -rf ${pkgdir}/usr/{bin,include,share}

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
