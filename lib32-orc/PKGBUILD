#
# Lib32 Packages for Chakra, part of chakra-project.org
#
# Maintainer: kote <koteccr at gmail dot com>
# Arch linux maintainer:
# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgbasename=orc
pkgname=lib32-${_pkgbasename}
pkgver=0.4.23
pkgrel=1
pkgdesc="The Oild Runtime Compiler. Multilib"
arch=('x86_64')
license=('custom')
url='http://code.entropywave.com/projects/orc/'
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('valgrind')
source=("http://gstreamer.freedesktop.org/data/src/orc/orc-${pkgver}.tar.xz")
sha256sums=('767eaebce2941737b43368225ec54598b3055ca78b4dc50c4092f5fcdc0bdfe7')

build() {
  cd orc-${pkgver}

  # multilib
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Removing unneeded files
  rm -rf ${pkgdir}/usr/{bin,include,share}

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
