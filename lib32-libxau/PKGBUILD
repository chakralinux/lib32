# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxau
pkgname=lib32-$_pkgbasename
pkgver=1.0.5
pkgrel=3
pkgdesc="X11 authorisation library"
url="http://xorg.freedesktop.org/"
arch=(x86_64)
depends=('lib32-glibc' $_pkgbasename)
makedepends=('pkgconfig' 'xproto>=7.0.15' 'gcc-multilib')
license=('custom')
options=('!libtool')
source=(${url}/releases/individual/lib/libXau-${pkgver}.tar.bz2)
md5sums=('993b3185c629e4b89401fca072dcb663')

build() {
  cd "${srcdir}/libXau-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXau-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
