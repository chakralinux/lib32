pkgname=lib32-libdbus
_pkgbasename=libdbus
pkgver=1.10.12
pkgrel=1
pkgdesc="DBus library (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/Software/dbus"
license=('GPL' 'custom')
depends=('lib32-glibc' 'lib32-expat' 'libdbus' $_pkgbasename=$pkgver)
makedepends=('lib32-libx11' 'lib32-gcc-libs')
provides=('lib32-dbus-core')
conflicts=('lib32-dbus-core')
replaces=('lib32-dbus-core')
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz)
md5sums=('bacef77336bbe588efa3124102d5fcdb')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/dbus-${pkgver}"

    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib32 \
        --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
        --with-system-pid-file=/run/dbus.pid \
        --with-console-auth-dir=/run/console/ \
        --enable-inotify \
        --disable-verbose-mode --disable-static \
        --disable-tests --disable-asserts --without-x \
        --disable-systemd
    make
}

package() {
    cd "${srcdir}/dbus-${pkgver}"
    make DESTDIR=${pkgdir} install

    rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
    rm -rf "${pkgdir}"/{etc,var}

    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
