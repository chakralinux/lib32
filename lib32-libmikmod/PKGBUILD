# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libmikmod
pkgname=lib32-$_pkgbasename
pkgver=3.2.0
pkgrel=1
pkgdesc="A portable sound library"
license=('GPL' 'LGPL')
url="http://sourceforge.net/projects/mikmod/"
arch=('x86_64')
depends=('lib32-glibc' "${_pkgbasename}=${pkgver}")
makedepends=('gcc-multilib')
options=('!libtool')
install=
source=(http://mikmod.shlomifish.org/files/${_pkgbasename}-${pkgver}.tar.gz
	libmikmod-3.2.0-64bit-fix.diff
        libmikmod-3.1.12-loopingvolume-fix.diff)
md5sums=('96e9820d72a41fe27ff304071739696c'
         '0bb09aac6e83e0a7fd6535961b3cff4c'
         'a837fd876cbd2ac27419b802504489db')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/$_pkgbasename-$pkgver

  # patches from sdl_mixer-1.2.11 source
  patch -Np1 -i $srcdir/$_pkgbasename-3.1.12-loopingvolume-fix.diff
  patch -Np1 -i $srcdir/libmikmod-3.2.0-64bit-fix.diff

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info --libdir=/usr/lib32
  make
}

package() {
  cd $srcdir/$_pkgbasename-$pkgver
  make DESTDIR=$pkgdir install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

