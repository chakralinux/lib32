# Lib32 packages for Chakra
#
# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributor from AUR: 3V0LU710N <db_eee-at-hotmail-dot-com>

_pkgbasename=libusbx
pkgname=lib32-$_pkgbasename
pkgver=1.0.14
pkgrel=2
pkgdesc="Library to enable user space application programs to communicate with USB devices. (32-bit). libusb fork."
arch=('x86_64')
url="http://libusbx.sourceforge.net/"
license=('LGPL')
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
provides=('lib32-libusb')
conflicts=('lib32-libusb')
replaces=('lib32-libusb')
source=(http://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)
options=(!libtool)
md5sums=('0a6a75edb4b4eae7dc82c1dd71ddc470')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "libusbx-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package () {
  cd "${srcdir}/libusbx-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}
