
# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=dbus-glib

pkgname=lib32-${_pkgbasename}
pkgver=0.100.2
pkgrel=2
pkgdesc="GLib bindings for DBUS (32-bit)"
arch=(x86_64)
license=('GPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('lib32-dbus-core' 'lib32-glib2' "${_pkgbasename}=${pkgver}")
makedepends=('gcc-multilib' 'lib32-libffi' 'pkgconfig')
options=('!emptydirs')
source=(http://dbus.freedesktop.org/releases/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('ad0920c7e3aad669163bb59171cf138e')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libdir=/usr/lib32 \
              --enable-static=no \
              --enable-bash-completion=no
  make
}

package() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    make DESTDIR=${pkgdir} install

    rm -rf "${pkgdir}"/usr/{bin,include,share}
}
