# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libcups
pkgname=lib32-$_pkgbasename
pkgver=1.4.7
pkgrel=1
pkgdesc="The CUPS Printing System - client libraries (32-bit)"
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=(lib32-glibc $_pkgbasename)
makedepends=(gcc-multilib)
source=(http://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2)
md5sums=('1590033ab4c739b859aeb672fe849089')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/cups-${pkgver}
  
  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-ldap \
              --enable-raw-printing \
              --disable-gssapi \
              --disable-dbus \
              --enable-ssl=no \
              --disable-gnutls \
              --disable-threads \
              --with-optim="$CFLAGS" \
              --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/cups-${pkgver}
  make BUILDROOT=${pkgdir} install-libs
}
