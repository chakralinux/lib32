# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libcups
pkgname=lib32-$_pkgbasename
pkgver=2.0.2
pkgrel=1
pkgdesc="The CUPS Printing System - client libraries (32-bit)"
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=(lib32-krb5 lib32-libtiff lib32-libpng lib32-gnutls $_pkgbasename=$pkgver)
makedepends=(gcc-multilib)
source=(#ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2
http://www.cups.org/software/${pkgver}/cups-${pkgver}-source.tar.bz2{,.sig})
md5sums=('6e0ea72dbafcf5baaa1cf4178e71096d'
         'SKIP')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/cups-${pkgver}

  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --enable-raw-printing \
              --disable-gssapi \
              --disable-dbus \
              --enable-threads \
              --enable-libusb=no \
              --with-optim="$CFLAGS" \
              --disable-avahi \
              --disable-dnssd \
              --libdir=/usr/lib32
  make libs
}

package() {
  cd ${srcdir}/cups-${pkgver}
  make BUILDROOT=${pkgdir} install-libs
  rm -rf "${pkgdir}/usr/lib"
}

