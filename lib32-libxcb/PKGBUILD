# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Calà <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxcb
pkgname=lib32-libxcb
pkgver=1.8.1
pkgrel=1
pkgdesc="X11 client-side library"
arch=(x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto' 'lib32-libxdmcp' 'lib32-libxau'
         $_pkgbasename)
makedepends=('pkgconfig' 'libxslt' 'python' 'gcc-multilib'
             'autoconf')
conflicts=('libx11<1.1.99.2')
options=('!libtool')
license=('custom')
source=(${url}/dist/${_pkgbasename}-${pkgver}.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch)
sha1sums=('98199b6054750a06cddd4e77baa4354af547ce6f'
          '93a5bb66ac8bd55e310c708addf5c711eb5e91f6')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
  ./configure --prefix=/usr --enable-xinput --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
