# Part of X.org group
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxcb
pkgname=lib32-libxcb
pkgver=1.9.3
pkgrel=1
pkgdesc="X11 client-side library (32-bit)"
arch=(x86_64)
url="http://xcb.freedesktop.org/"
depends=('lib32-libxdmcp' 'lib32-libxau'
         $_pkgbasename=$pkgver)
makedepends=('pkg-config' 'libxslt' 'python2' 'gcc-multilib' 'libtool-multilib'
             'autoconf')
conflicts=('libx11<1.1.99.2')
options=('!libtool')
license=('custom')
source=(${url}/dist/${_pkgbasename}-${pkgver}.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch)
sha1sums=('28076500e2ca05e5887c7ed231ba305a85e8bd0b'
          '93330bdcf29a9386b4222e2f0c3cea3add93f5c0')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -p1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  PYTHON=/usr/bin/python2 ./autogen.sh \
	  --prefix=/usr \
	  --enable-xinput \
	  --libdir=/usr/lib32 \
	  --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
