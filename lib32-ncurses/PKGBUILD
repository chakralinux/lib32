_pkgbasename=ncurses
pkgname=lib32-${_pkgbasename}
pkgver=6.0
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library (32-bit)"
arch=('x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('lib32-gcc-libs' $_pkgbasename=$pkgver)
makedepends=("gcc")
source=(https://ftp.gnu.org/pub/gnu/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # Thomas Dickey
md5sums=('ee13d052e1ead260d7c28071f46eefb1'
         'SKIP')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --libdir=/usr/lib32 \
              --with-install-prefix=${pkgdir} \
              --with-shared \
              --with-normal \
              --without-debug \
              --without-ada \
              --enable-widec \
              --enable-pc-files \
              --with-cxx-binding --with-cxx-shared \
              --enable-ext-colors --enable-ext-mouse
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}
  make install

  install -dm755 ${pkgdir}/usr/lib32

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in curses ncurses form panel menu; do
    rm -f ${pkgdir}/usr/lib32/lib${lib}.so
    echo "INPUT(-l${lib}w)" >${pkgdir}/usr/lib32/lib${lib}.so
    ln -sf lib${lib}w.a ${pkgdir}/usr/lib32/lib${lib}.a
  done
  ln -sf libncurses++w.a ${pkgdir}/usr/lib32/libncurses++.a

  # some packages look for -lcurses during build
  rm -f ${pkgdir}/usr/lib32/libcursesw.so
  echo "INPUT(-lncursesw)" >${pkgdir}/usr/lib32/libcursesw.so
  ln -sf libncurses.so ${pkgdir}/usr/lib32/libcurses.so
  ln -sf libncursesw.a ${pkgdir}/usr/lib32/libcursesw.a
  ln -sf libncurses.a ${pkgdir}/usr/lib32/libcurses.a

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
