_pkgbasename=libssh2
pkgname=lib32-$_pkgbasename
pkgver=1.7.0
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (32-bit)"
arch=('x86_64')
url="http://www.libssh2.org/"
license=('BSD')
depends=('lib32-openssl' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
options=('!libtool')
source=("http://www.libssh2.org/download/$_pkgbasename-$pkgver.tar.gz")
md5sums=('b01662a210e94cccf2f76094db7dac5c')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure \
      --prefix=/usr \
      --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{share,bin,sbin,include}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
