# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libssh2
pkgname=lib32-$_pkgbasename
pkgver=1.4.3
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (32-bit)"
arch=('x86_64')
url="http://www.libssh2.org/"
license=('BSD')
depends=('lib32-zlib' 'lib32-libgcrypt' 'lib32-openssl' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=("http://www.libssh2.org/download/$_pkgbasename-$pkgver.tar.gz")
md5sums=('071004c60c5d6f90354ad1b701013a0b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure \
      --with-libgcrypt \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{share,bin,include}
}
