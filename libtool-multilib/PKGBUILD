# maintainer: Manuel Tortosa

# NOTE: requires rebuilt with each new gcc version

pkgbase=libtool-multilib
_pkgbase=libtool
pkgname=(libtool-multilib lib32-libltdl)
pkgver=2.4.2
pkgrel=4
pkgdesc="A generic library support script for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
makedepends=(sh tar texinfo gcc-multilib)
provides=('libltdl' 'libtool')
options=('!libtool')
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${_pkgbase}-${pkgver}.tar.xz{,.sig})
md5sums=('2ec8997e0c07249eb4cbd072417d70fe'
         '1e6ba57420c82c663c85e745d11c7eed')

build() {
  cd "$srcdir"

  rm -rf libtool-64 libtool-32
  mv libtool-$pkgver libtool-64
  cp -a libtool-64 libtool-32

  msg2 "Building libtool-64..."
  cd "$srcdir/libtool-64"
  ./configure --prefix=/usr
  make

  msg2 "Building libtool-32..."
  export CC="gcc -m32"
  export CXX="g++ -m32"
  cd "$srcdir/libtool-32"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package_libtool-multilib() {
  depends=('sh' "libltdl=$pkgver" 'tar' "gcc-multilib=4.8.0" "lib32-libltdl=$pkgver")
  groups=('multilib-devel')
  install=libtool.install
  provides=("libtool=$pkgver-$pkgrel")
  conflicts=(libtool)

  cd "$srcdir/libtool-64"

  make DESTDIR=${pkgdir} install-binSCRIPTS install-man install-info \
                         install-data-local
  rm -rf ${pkgdir}/usr/share/libtool/libltdl/
}

package_lib32-libltdl() {
  pkgdesc="A system independent dlopen wrapper for GNU libtool (32-bit)"
  depends=(lib32-glibc libltdl)
  replaces=(lib32-libtool)
  provides=("lib32-libtool=$pkgver-$pkgrel")
  conflicts=(lib32-libtool)

  cd "$srcdir/libtool-32"
  make DESTDIR="$pkgdir" install-libLTLIBRARIES
}
