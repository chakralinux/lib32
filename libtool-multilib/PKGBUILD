# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

# NOTE: requires rebuilt with each new gcc version

pkgbase=libtool-multilib
_pkgbase=libtool
pkgname=(libtool-multilib lib32-libtool)
pkgver=2.4.2
pkgrel=1
pkgdesc="A generic library support script for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
makedepends=(sh tar texinfo gcc-multilib)
provides=('libltdl' 'libtool')
options=('!libtool')
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${_pkgbase}-${pkgver}.tar.xz{,.sig})
md5sums=('2ec8997e0c07249eb4cbd072417d70fe'
         '1e6ba57420c82c663c85e745d11c7eed')

build() {
  cd "$srcdir"

  rm -rf libtool-64 libtool-32
  mv libtool-$pkgver libtool-64
  cp -a libtool-64 libtool-32

msg2 "Building libtool-64..."

  cd "$srcdir/libtool-64"
  ./configure --prefix=/usr
  make
  make check

msg2 "Building libtool-32..."

  export CC="gcc -m32"
  export CXX="g++ -m32"

  cd "$srcdir/libtool-32"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
  make check
}

package_libtool-multilib() {
  groups=('base-devel-multilib')
  depends=(sh tar texinfo "lib32-libtool=$pkgver-$pkgrel")
  provides=("libtool=$pkgver-$pkgrel")
  conflicts=(libtool)
  install=libtool.install

  cd "$srcdir/libtool-64"
  make DESTDIR="$pkgdir" install
}

package_lib32-libtool() {
  pkgdesc="A generic library support script (32-bit)"
  depends=(lib32-glibc)

  cd "$srcdir/libtool-32"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
