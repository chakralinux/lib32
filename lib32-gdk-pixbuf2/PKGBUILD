# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=gdk-pixbuf2
pkgname=lib32-$_pkgbasename
pkgver=2.30.4
pkgrel=2
pkgdesc="An image loading library for gtk2 (32-bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('lib32-glib2'
          lib32-lib{'png','tiff','jpeg','ffi','x11'}
          "$_pkgbasename=$pkgver")
makedepends=(gcc-multilib)
options=('!docs')
install=gdk-pixbuf2.install
source=("http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-${pkgver}.tar.xz")
sha1sums=('1f92ef9246ed70003cbb104128c0d24720830d53')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr  \
              --libdir=/usr/lib32 \
              --without-libjasper \
              --with-x11 \
              --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gdk-pixbuf-query-loaders gdk-pixbuf-query-loaders-32
  rm gdk-pixbuf-csource
  rm gdk-pixbuf-pixdata
}

