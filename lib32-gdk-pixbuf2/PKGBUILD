# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=gdk-pixbuf2
pkgname=lib32-$_pkgbasename
pkgver=2.33.1
pkgrel=1
pkgdesc="An image loading library (32-bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=(lib32-glib2 lib32-libpng lib32-libtiff lib32-libjpeg lib32-libx11
         "$_pkgbasename=$pkgver")
makedepends=(gcc-multilib)
options=('!docs')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-${pkgver}.tar.xz)
sha256sums=('132358b39bd8204e2ef0571c14206dfdca1f44bcc94c21751d6e6b0433b5088b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr  --libdir=/usr/lib32 \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gdk-pixbuf-query-loaders gdk-pixbuf-query-loaders-32
  rm gdk-pixbuf-pixdata
  rm gdk-pixbuf-csource
}

# vim:set ts=2 sw=2 et:
