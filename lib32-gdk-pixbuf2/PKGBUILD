_pkgbasename=gdk-pixbuf2
pkgname=lib32-$_pkgbasename
pkgver=2.36.3
pkgrel=1
pkgdesc="An image loading library for gtk2 (32-bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('lib32-glib2'
          lib32-lib{'png','tiff','jpeg','ffi','x11'}
          $_pkgbasename)
makedepends=('gcc' 'lib32-gcc-libs')
options=('!docs')
install=gdk-pixbuf2.install
source=(https://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-${pkgver}.tar.xz)
sha256sums=('5223138f7d31afc6b356a049930304ec0abd6ac1113a5d3d1dba5cd4a4d639ec')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr  --libdir=/usr/lib32 \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gdk-pixbuf-query-loaders gdk-pixbuf-query-loaders-32
  rm gdk-pixbuf-csource
  rm gdk-pixbuf-pixdata
}

